import algebra.homology.homotopy
import category_theory.abelian.basic

universes v u

noncomputable theory

open category_theory category_theory.limits

variables {Œπ‚ÇÅ Œπ‚ÇÇ : Type*}

namespace complex_shape

structure embedding (c‚ÇÅ : complex_shape Œπ‚ÇÅ) (c‚ÇÇ : complex_shape Œπ‚ÇÇ) :=
(f : Œπ‚ÇÅ ‚Üí Œπ‚ÇÇ)
(r : Œπ‚ÇÇ ‚Üí option Œπ‚ÇÅ)
(eq_some : ‚àÄ i i', r i = some i' ‚Üî f i' = i)
(c : ‚àÄ ‚¶Éi j‚¶Ñ, c‚ÇÅ.rel i j ‚Üí c‚ÇÇ.rel (f i) (f j))

namespace embedding

def pos_int_to_onat : ‚Ñ§ ‚Üí option ‚Ñï
| (n:‚Ñï)  := n
| -[1+n] := none

def neg_int_to_onat : ‚Ñ§ ‚Üí option ‚Ñï
| 0       := (0:‚Ñï)
| (n+1:‚Ñï) := none
| -[1+n]  := (n+1:‚Ñï)

def nat_up_int_up : embedding (complex_shape.up ‚Ñï) (complex_shape.up ‚Ñ§) :=
{ f := coe,
  r := pos_int_to_onat,
  eq_some := begin
    rintro (i|i) i',
    { split; { rintro ‚ü®rfl‚ü©, refl }, },
    { split; { rintro ‚ü®‚ü©, } }
  end,
  c := by { rintro i j (rfl : _ = _), dsimp, refl } }

def nat_down_int_down : embedding (complex_shape.down ‚Ñï) (complex_shape.down ‚Ñ§) :=
{ f := coe,
  r := pos_int_to_onat,
  eq_some := begin
    rintro (i|i) i',
    { split; { rintro ‚ü®rfl‚ü©, refl }, },
    { split; { rintro ‚ü®‚ü©, } }
  end,
  c := by { rintro i j (rfl : _ = _), dsimp, refl } }

def nat_down_int_up : embedding (complex_shape.down ‚Ñï) (complex_shape.up ‚Ñ§) :=
{ f := -coe,
  r := neg_int_to_onat,
  eq_some := begin
    rintro ((_|i)|i) (_|i'),
    any_goals { split; { rintro ‚ü®‚ü©, } },
    any_goals { split; { rintro ‚ü®rfl‚ü©, refl }, },
  end,
  c := by { rintro i j (rfl : _ = _),
    simp only [pi.neg_apply, int.coe_nat_succ, neg_add_rev, up_rel, neg_add_cancel_comm], } }

def nat_up_int_down : embedding (complex_shape.up ‚Ñï) (complex_shape.down ‚Ñ§) :=
{ f := -coe,
  r := neg_int_to_onat,
  eq_some := begin
    rintro ((_|i)|i) (_|i'),
    any_goals { split; { rintro ‚ü®‚ü©, } },
    any_goals { split; { rintro ‚ü®rfl‚ü©, refl }, },
  end,
  c := by { rintro i j (rfl : _ = _),
    simp only [pi.neg_apply, int.coe_nat_succ, neg_add_rev, down_rel, neg_add_cancel_comm] } }

end embedding

end complex_shape

variables {c‚ÇÅ : complex_shape Œπ‚ÇÅ} {c‚ÇÇ : complex_shape Œπ‚ÇÇ}
variables {C : Type*} [category C] [abelian C]

namespace homological_complex

open_locale zero_object

variables (e : c‚ÇÅ.embedding c‚ÇÇ)
variables (X Y Z : homological_complex C c‚ÇÅ) (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

def embed.X : option Œπ‚ÇÅ ‚Üí C
| (some i) := X.X i
| none     := 0

def embed.d : Œ† i j, embed.X X i ‚ü∂ embed.X X j
| (some i) (some j) := X.d i j
| (some i) none     := 0
| none     j        := 0

lemma embed.shape : ‚àÄ (i j : option Œπ‚ÇÅ)
  (h : ‚àÄ (i' j' : Œπ‚ÇÅ), i = some i' ‚Üí j = some j' ‚Üí ¬¨ c‚ÇÅ.rel i' j'),
  embed.d X i j = 0
| (some i) (some j) h := X.shape _ _ $ h i j rfl rfl
| (some i) none     h := rfl
| none     j        h := rfl

lemma embed.d_comp_d : ‚àÄ i j k, embed.d X i j ‚â´ embed.d X j k = 0
| (some i) (some j) (some k) := X.d_comp_d _ _ _
| (some i) (some j) none     := comp_zero
| (some i) none     k        := comp_zero
| none     j        k        := zero_comp

def embed.obj : homological_complex C c‚ÇÇ :=
{ X := embed.X X ‚àò e.r,
  d := Œª i j, embed.d X (e.r i) (e.r j),
  shape' := Œª i j hij, embed.shape X _ _ begin
    simp only [e.eq_some],
    rintro i' j' rfl rfl h',
    exact hij (e.c h')
  end,
  d_comp_d' := Œª i j k hij hjk, embed.d_comp_d X _ _ _ }

variables {X Y Z}

def embed.f : Œ† i, embed.X X i ‚ü∂ embed.X Y i
| (some i) := f.f i
| none     := 0

lemma embed.comm :  ‚àÄ i j, embed.f f i ‚â´ embed.d Y i j = embed.d X i j ‚â´ embed.f f j
| (some i) (some j) := f.comm _ _
| (some i) none     := show _ ‚â´ 0 = 0 ‚â´ 0, by simp only [comp_zero]
| none     j        := show 0 ‚â´ 0 = 0 ‚â´ _, by simp only [zero_comp]

def embed.map : embed.obj e X ‚ü∂ embed.obj e Y :=
{ f := Œª i, embed.f f _,
  comm' := Œª i j hij, embed.comm f _ _ }

lemma embed.f_id : ‚àÄ i, embed.f (ùüô X) i = ùüô (embed.X X i)
| (some i) := rfl
| none     := has_zero_object.from_zero_ext _ _

lemma embed.f_comp : ‚àÄ i, embed.f (f ‚â´ g) i = embed.f f i ‚â´ embed.f g i
| (some i) := rfl
| none     := has_zero_object.from_zero_ext _ _

def embed : homological_complex C c‚ÇÅ ‚•§ homological_complex C c‚ÇÇ :=
{ obj := embed.obj e,
  map := Œª X Y f, embed.map e f,
  map_id' := Œª X, by { ext i, exact embed.f_id _ },
  map_comp' := by { intros, ext i, exact embed.f_comp f g _ } }

end homological_complex
