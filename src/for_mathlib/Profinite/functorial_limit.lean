import for_mathlib.arrow
import for_mathlib.Fintype.basic
import for_mathlib.Profinite.limits
import for_mathlib.Profinite.basic
import for_mathlib.Fintype.basic
import topology.locally_constant.basic
import category_theory.limits.functor_category

/-!
Let `X` and `Y` be profinite sets and `f : X ‚ü∂ Y` a morphism.
We show:
1. That `X` is a limit of finite sets.
2. That `f` is a limit of morphisms of finite sets,
  when considered as an object in the arrow category.
-/

open_locale classical

universe u
open category_theory

noncomputable theory

namespace Profinite

/--
This is the type whose terms are decompositions of `X` into
disjoint unions of nonempty clopen sets.
This is endowed with a coercion to type, so one can write
`U : I` given `I : X.clopen_cover`, meaning that `U` is one of the sets
appearing in the clopen cover `I`.
-/
@[ext]
structure clopen_cover (X : Profinite.{u}) :=
(sets : set (set X))
(clopen : ‚àÄ S : sets, is_clopen (S : set X))
(nonempty : ‚àÄ S : sets, (S : set X).nonempty)
(cover : ‚àÄ x : X, ‚àÉ! U: sets, x ‚àà (U : set X))

namespace clopen_cover

variable {X : Profinite.{u}}

/-- 
Construct a term of `X.clopen_cover` given a nonempty clopen set of `X` whose
complement is nonempty.
-/
def of_clopen {U : set X} :
  is_clopen U ‚Üí U.nonempty ‚Üí U·∂ú.nonempty ‚Üí X.clopen_cover := Œª h1 h2 h3,
{ sets := {U,U·∂ú},
  clopen := begin
    rintro ‚ü®V,rfl|h‚ü©, { assumption },
    rw set.mem_singleton_iff at h,
    simp [h, is_clopen_compl h1],
  end,
  nonempty := begin
    rintro ‚ü®V,rfl|h‚ü©, { assumption },
    rw set.mem_singleton_iff at h,
    simpa [h],
  end ,
  cover := begin
    rintro x,
    by_cases hx : x ‚àà U,
    { refine ‚ü®‚ü®U,or.inl rfl‚ü©, hx, _‚ü©,
      rintros ‚ü®V,rfl|hV‚ü© hhV, { refl },
      rw set.mem_singleton_iff at hV,
      ext1,
      dsimp at hhV,
      rw hV at hhV,
      exact false.elim (hhV hx) },
    { refine ‚ü®‚ü®U·∂ú, or.inr rfl‚ü©, hx, _‚ü©,
      rintros ‚ü®V,rfl|hV‚ü© hhV,
      { exact false.elim (hx hhV) },
      { simpa using hV } }
  end }

instance : has_coe_to_sort X.clopen_cover := ‚ü®Type*, Œª I, I.sets‚ü©

instance {I : X.clopen_cover} : topological_space I := ‚ä•

lemma is_clopen {I : X.clopen_cover} (U : I) :
  is_clopen (U : set X) := (I.clopen _)

lemma is_open {I : X.clopen_cover} (U : I) :
  is_open (U : set X) := (I.clopen _).1

lemma is_closed {I : X.clopen_cover} (U : I) :
  is_closed (U : set X) := (I.clopen _).2

lemma eq_of_le {I : X.clopen_cover} (U V : I) : (U : set X) ‚â§ V ‚Üí U = V :=
begin
  intro h,
  rcases (I.nonempty U) with ‚ü®y,hy‚ü©,
  rcases I.cover y with ‚ü®W,h1,h2‚ü©,
  rw [h2 U hy, h2 V (h hy)],
end

instance {I : X.clopen_cover} : fintype I :=
begin
  have h : _root_.is_compact (‚ä§ : set X) := compact_univ,
  rw compact_iff_finite_subcover at h,
  specialize h (Œª i : I, i) (Œª i, is_open _) (Œª x _, _),
  rcases I.cover x with ‚ü®U,hU,hU2‚ü©,
  refine ‚ü®U,‚ü®U,rfl‚ü©,hU‚ü©,
  let S := classical.some h,
  let hS := classical.some_spec h,
  refine ‚ü®S,_‚ü©,
  intros U,
  rcases I.nonempty U with ‚ü®x,hx‚ü©,
  specialize hS (by tauto : x ‚àà _),
  rcases hS with ‚ü®V,‚ü®W,rfl‚ü©,W,‚ü®(h1 : _ ‚àà S),rfl‚ü©,h2‚ü©,
  dsimp at h2,
  suffices : U = W, by rwa this,
  rcases I.cover x with ‚ü®S,hS,hh‚ü©,
  rw [hh U hx, hh W h2],
end

/-- The "trivial" clopen cover. -/
def top : X.clopen_cover :=
if h : _root_.nonempty X then
‚ü®{‚ä§}, by simp, begin
  rcases h with ‚ü®x‚ü©,
  rintro ‚ü®h,hh‚ü©,
  refine ‚ü®x,_‚ü©,
  simp only [set.mem_singleton_iff] at hh,
  simp [hh]
end,by tidy‚ü©
else
‚ü®‚àÖ,by simp, by simp, Œª x, false.elim (h ‚ü®x‚ü©)‚ü©

lemma top_def : (top : X.clopen_cover) =
if h : _root_.nonempty X then
‚ü®{‚ä§}, by simp, begin
  rcases h with ‚ü®x‚ü©,
  rintro ‚ü®h,hh‚ü©,
  refine ‚ü®x,_‚ü©,
  simp only [set.mem_singleton_iff] at hh,
  simp [hh]
end, by tidy‚ü©
else
‚ü®‚àÖ, by simp, by simp, Œª x, false.elim (h ‚ü®x‚ü©)‚ü© := rfl

instance : has_top X.clopen_cover := ‚ü®top‚ü©
instance : inhabited X.clopen_cover := ‚ü®‚ä§‚ü©

/-- 
The "canonical" term of `clopen_cover.of_clopen`, whose underlying set is the given clopen set. 
-/
def of_clopen.mk {U : set X} {h1 : _root_.is_clopen U} {h2 : U.nonempty} {h3 : U·∂ú.nonempty} :
  of_clopen h1 h2 h3 := ‚ü®U, or.inl rfl‚ü©

/-- The coarsest common refinement of two clopen covers. -/
def common (I J : X.clopen_cover) : X.clopen_cover :=
{ sets := { U | (U : set X).nonempty ‚àß ‚àÉ (A : I) (B : J), (U : set X) = A ‚äì B },
  clopen := begin
    rintro ‚ü®U,hU1,‚ü®A,B,rfl‚ü©‚ü©,
    exact is_clopen_inter (clopen _ _) (clopen _ _),
  end,
  nonempty := Œª U, U.2.1,
  cover := begin
    intro x,
    rcases I.cover x with ‚ü®A,hA1,hA2‚ü©,
    rcases J.cover x with ‚ü®B,hB1,hB2‚ü©,
    refine ‚ü®‚ü®(A ‚äì B : set X),‚ü®x,hA1,hB1‚ü©,‚ü®A,B,rfl‚ü©‚ü©,‚ü®hA1,hB1‚ü©,_‚ü©,
    rintros ‚ü®W,‚ü®W,A',B',rfl‚ü©‚ü© hW2,
    have : A' = A,
    { apply hA2,
      exact hW2.1 },
    subst this,
    have : B' = B,
    { apply hB2,
      exact hW2.2 },
    subst this,
  end }

/-- 
`le_rel f I J`, where `f : X ‚ü∂ Y`, `I : X.clopen_cover` and
`J : Y.clopen_cover` means, mathematically, that
`I` refines the pullback of `J` with respect to `f`.
-/
def le_rel {X Y : Profinite.{u}} (f : X ‚ü∂ Y)
  (I : X.clopen_cover) (J : Y.clopen_cover) : Prop :=
‚àÄ U : I, ‚àÉ V : J, (U : set X) ‚â§ f ‚Åª¬π' V

lemma le_rel_top {X Y : Profinite.{u}} (f : X ‚ü∂ Y) (I : X.clopen_cover) :
  le_rel f I ‚ä§ :=
begin
  change le_rel f I top,
  intros U,
  rcases I.nonempty U with ‚ü®x,hx‚ü©,
  rw top_def,
  refine ‚ü®‚ü®‚ä§,_‚ü©,Œª x, by tauto‚ü©,
  split_ifs,
  { simp },
  { exact false.elim (h ‚ü®f x‚ü©) },
end

/-- 
Given `h : le_refl f I J`, this provides the canonical map `I ‚Üí J`.
-/
def map {X Y : Profinite.{u}} {f : X ‚ü∂ Y} {I : X.clopen_cover}
  {J : Y.clopen_cover} (h : le_rel f I J) : I ‚Üí J :=
Œª U, classical.some (h U)

lemma map_spec {X Y : Profinite.{u}} {f : X ‚ü∂ Y} {I : X.clopen_cover}
  {J : Y.clopen_cover} (h : le_rel f I J) (U : I) :
  (U : set X) ‚â§ f ‚Åª¬π' (map h U) := classical.some_spec (h U)

lemma map_unique {X Y : Profinite.{u}} {f : X ‚ü∂ Y} {I : X.clopen_cover}
  {J : Y.clopen_cover} (h : le_rel f I J) (U : I) (V : J) :
  (U : set X) ‚â§ f ‚Åª¬π' V ‚Üí V = map h U :=
begin
  intro hh,
  rcases (I.nonempty U) with ‚ü®x,hx‚ü©,
  rcases J.cover (f x) with ‚ü®W,hW1,hW2‚ü©,
  rw [hW2 V (hh hx), hW2 (map h U) (map_spec _ _ hx)],
end

lemma le_rel_refl (I : X.clopen_cover) : le_rel (ùüô X) I I := Œª U, ‚ü®U, by simp‚ü©

lemma le_rel_comp {X Y Z : Profinite.{u}} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}
  {I : X.clopen_cover} {J : Y.clopen_cover} {K : Z.clopen_cover} :
  le_rel f I J ‚Üí le_rel g J K ‚Üí le_rel (f ‚â´ g) I K :=
begin
  intros h1 h2 U,
  rcases h1 U with ‚ü®V,hV‚ü©,
  rcases h2 V with ‚ü®W,hW‚ü©,
  refine ‚ü®W,le_trans hV _‚ü©,
  dsimp,
  conv_rhs { rw set.preimage_comp },
  exact set.preimage_mono hW,
end

@[simp]
lemma map_id {X : Profinite.{u}} {I : X.clopen_cover} (U : I) :
  map (le_rel_refl I) U = U :=
begin
  symmetry,
  apply map_unique,
  simp,
end

@[simp]
lemma map_comp {X Y Z : Profinite.{u}} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}
  {I : X.clopen_cover} {J : Y.clopen_cover} {K : Z.clopen_cover}
  (h1 : le_rel f I J) (h2 : le_rel g J K) (U : I) :
  map (le_rel_comp h1 h2) U = map h2 (map h1 U) :=
begin
  symmetry,
  apply map_unique,
  refine le_trans (map_spec h1 U) _,
  dsimp,
  conv_rhs {rw set.preimage_comp},
  apply set.preimage_mono,
  apply map_spec,
end


lemma common_le_rel_left {I J : X.clopen_cover} : le_rel (ùüô _) (common I J) I :=
begin
  rintro ‚ü®U, ‚ü®U, A, B, rfl‚ü©‚ü©,
  refine ‚ü®A, set.inter_subset_left _ _‚ü©,
end

lemma common_le_rel_right {I J : X.clopen_cover} : le_rel (ùüô _) (common I J) J :=
begin
  rintro ‚ü®U, ‚ü®U, A, B, rfl‚ü©‚ü©,
  refine ‚ü®B, set.inter_subset_right _ _‚ü©,
end

instance : semilattice_inf X.clopen_cover :=
{ inf := Œª I J, common I J,
  le := Œª I J, le_rel (ùüô _) I J,
  le_refl := Œª I, le_rel_refl _,
  le_trans := Œª I J K h1 h2, by simpa using le_rel_comp h1 h2,
  le_antisymm := begin
    intros I J h1 h2,
    ext S,
    split,
    { intro hS,
      rcases h1 ‚ü®S,hS‚ü© with ‚ü®V,hV‚ü©,
      have : S = V,
      { apply le_antisymm hV,
        rcases h2 V with ‚ü®W,hW‚ü©,
        have : W = ‚ü®S,hS‚ü©,
        { symmetry,
          apply eq_of_le,
          refine le_trans hV hW },
        rwa ‚Üê this },
      rw this,
      exact V.2 },
    { intro hS,
      rcases h2 ‚ü®S,hS‚ü© with ‚ü®V,hV‚ü©,
      have : S = V,
      { apply le_antisymm hV,
        rcases h1 V with ‚ü®W,hW‚ü©,
        have : W = ‚ü®S,hS‚ü©,
        { symmetry,
          apply eq_of_le,
          refine le_trans hV hW },
        rwa ‚Üê this },
      rw this,
      exact V.2 }
  end,
  inf_le_left := Œª I J, common_le_rel_left,
  inf_le_right := Œª I J, common_le_rel_right,
  le_inf := begin
    intros I J K h1 h2 U,
    rcases h1 U with ‚ü®A,hA‚ü©,
    rcases h2 U with ‚ü®B,hB‚ü©,
    simp only [set.preimage_id, Profinite.id_to_fun, set.le_eq_subset] at hA hB,
    obtain ‚ü®x,hx‚ü© := I.nonempty U,
    refine ‚ü®‚ü®A ‚äì B, ‚ü®x, hA hx, hB hx‚ü©, A, B, rfl‚ü©, _‚ü©,
    simp only [set.preimage_id,
      Profinite.id_to_fun,
      set.subset_inter_iff,
      subtype.coe_mk,
      set.le_eq_subset,
      set.inf_eq_inter],
    refine ‚ü®hA,hB‚ü©,
  end }

lemma inf_mono_left {I J K : X.clopen_cover} : J ‚â§ K ‚Üí J ‚äì I ‚â§ K ‚äì I :=
begin
  rintros h ‚ü®U,‚ü®hU,A,B,rfl‚ü©‚ü©,
  rcases h A with ‚ü®AA,hAA‚ü©,
  simp only [set.preimage_id, Profinite.id_to_fun, set.le_eq_subset] at *,
  have : (A : set X) ‚äì B ‚â§ AA ‚äì B := Œª x ‚ü®h1,h2‚ü©, ‚ü®hAA h1,h2‚ü©,
  refine ‚ü®‚ü®AA ‚äì B,set.nonempty.mono this hU, AA, B, rfl‚ü©,this‚ü©,
end

lemma inf_mono_right {I J K : X.clopen_cover} : J ‚â§ K ‚Üí I ‚äì J ‚â§ I ‚äì K :=
begin
  rintros h ‚ü®U,‚ü®hU,A,B,rfl‚ü©‚ü©,
  rcases h B with ‚ü®BB,hBB‚ü©,
  simp only [set.preimage_id, Profinite.id_to_fun, set.le_eq_subset] at *,
  have : (A : set X) ‚äì B ‚â§ A ‚äì BB := Œª x ‚ü®h1,h2‚ü©, ‚ü®h1, hBB h2‚ü©,
  refine ‚ü®‚ü®A ‚äì BB, set.nonempty.mono this hU, A, BB, rfl‚ü©, this‚ü©
end

section pullback

variables {Y : Profinite.{u}} (f : Y ‚ü∂ X)

/-- The pullback of a clopen cover w.r.t. a morphism. -/
def pullback : X.clopen_cover ‚Üí Y.clopen_cover := Œª I,
{ sets := { A | A.nonempty ‚àß ‚àÉ U : I, A = f ‚Åª¬π' U },
  clopen := begin
    rintro ‚ü®A, h1, U, rfl‚ü©,
    exact ‚ü®is_open.preimage f.continuous (is_open _),
      is_closed.preimage f.continuous (is_closed _)‚ü©,
  end,
  nonempty := Œª U, U.2.1,
  cover := begin
    intro y,
    rcases I.cover (f y) with ‚ü®U,hU1,hU2‚ü©,
    refine ‚ü®‚ü®f ‚Åª¬π' U, ‚ü®y, hU1‚ü©, U, rfl‚ü©, hU1, _‚ü©,
    rintro ‚ü®V,‚ü®hV,V,rfl‚ü©‚ü© hhV,
    suffices : V = U, by tidy,
    exact hU2 _ hhV,
  end }

variable {f}

lemma pullback_mono {I J : X.clopen_cover} (h : I ‚â§ J) : pullback f I ‚â§ pullback f J :=
begin
  rintro ‚ü®U,‚ü®hU,U,rfl‚ü©‚ü©,
  rcases h U with ‚ü®J,hJ‚ü©,
  refine ‚ü®‚ü®f ‚Åª¬π' J, ‚ü®_, J, rfl‚ü©‚ü©, set.preimage_mono hJ‚ü©,
  exact set.nonempty.mono (set.preimage_mono hJ) hU,
end

lemma pullback_spec {I : X.clopen_cover} (U : pullback f I) : ‚àÉ! V : I, (U : set Y) = f ‚Åª¬π' V :=
begin
  rcases U with ‚ü®U,‚ü®h,V,rfl‚ü©‚ü©,
  refine ‚ü®V, rfl, _‚ü©,
  intros W hW,
  rcases h with ‚ü®y,hy‚ü©,
  rcases I.cover (f y) with ‚ü®A,hA1,hA2‚ü©,
  dsimp at hW,
  have hy' := hy,
  rw hW at hy,
  rw [hA2 W hy, hA2 V hy'],
end

-- TODO: Move if needed.
/-
lemma pullback_proj {I : X.clopen_cover} (y : Y) : ((pullback f I).proj y : set Y) = f ‚Åª¬π' (I.proj (f y)) :=
begin
  rcases pullback_spec ((pullback f I).proj y) with ‚ü®V,h1,h2‚ü©,
  erw h1,
  congr,
  apply proj_fun_unique,
  change y ‚àà f ‚Åª¬π' V,
  rw ‚Üê h1,
  apply proj_fun_spec,
end
-/

lemma pullback_id {I : X.clopen_cover} : pullback (ùüô X) I = I :=
begin
  ext S,
  dsimp [pullback],
  split,
  { rintro ‚ü®‚ü®z,hz‚ü©,‚ü®U,hU‚ü©‚ü©,
    simp [hU] },
  { intro h,
    refine ‚ü®I.nonempty ‚ü®S,h‚ü©, ‚ü®S,h‚ü©, rfl‚ü© }
end

lemma pullback_comp {X Y Z : Profinite.{u}} {I : Z.clopen_cover} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
  pullback (f ‚â´ g) I = pullback f (pullback g I) :=
begin
  ext S,
  dsimp [pullback],
  split,
  { rintro ‚ü®h1,U,hU‚ü©,
    refine ‚ü®h1,_‚ü©,
    rcases h1 with ‚ü®x,hx‚ü©,
    use g ‚Åª¬π' (U : set Z),
    dsimp,
    refine ‚ü®_,_‚ü©,
    { rw hU at hx,
      simp only [set.mem_preimage, function.comp_app] at hx,
      refine ‚ü®f x, hx‚ü© },
    { use U },
    { simpa using hU, } },
  { rintro ‚ü®‚ü®x,hx‚ü©,‚ü®U,hU1,‚ü®V,rfl‚ü©‚ü©,rfl‚ü©,
    refine ‚ü®‚ü®x,hx‚ü©,_‚ü©,
    refine ‚ü®V,_‚ü©,
    refl }
end

lemma pullback_le_rel (I : X.clopen_cover) : le_rel f (pullback f I) I :=
begin
  rintros ‚ü®U,hU1,V,rfl‚ü©,
  refine ‚ü®V,le_refl _‚ü©,
end

lemma pullback_map_injective {B : Profinite} (f : X ‚ü∂ B) (I : B.clopen_cover) :
  function.injective (clopen_cover.map I.pullback_le_rel : I.pullback f ‚Üí I) :=
begin
  intros U V h,
  apply clopen_cover.eq_of_le,
  intros a ha,
  have hU := clopen_cover.map_spec (I.pullback_le_rel : clopen_cover.le_rel f _ _) U ha,
  rw h at hU,
  rcases (clopen_cover.pullback_spec V) with ‚ü®W,h1,h2‚ü©,
  rw h1,
  convert hU,
  apply clopen_cover.map_unique,
  refine le_of_eq h1,
end

end pullback

section proj

/-!
Given `I : X.cl`, `proj I` is the function `X ‚Üí I` sending `x` to the unique
memeber of `I` in which it's contained.
-/

/-- The function underlying the canonical projection `X ‚ü∂ I` for `I : X.clopen_cover`. -/
def proj_fun (I : X.clopen_cover) : X ‚Üí I := Œª x, classical.some (I.cover x)

lemma proj_fun_spec (I : X.clopen_cover) (x : X) : x ‚àà (proj_fun I x : set X) :=
  (classical.some_spec (I.cover x)).1

lemma proj_fun_unique (I : X.clopen_cover) (x : X) (U : I) :
  x ‚àà (U : set X) ‚Üí U = proj_fun I x :=
begin
  intro h,
  rcases I.cover x with ‚ü®V,hV,hh‚ü©,
  rw [hh U h, hh (proj_fun I x) (proj_fun_spec _ _)],
end

lemma proj_fun_mem (I : X.clopen_cover) (x y : X) :
  x ‚àà (proj_fun I y : set X) ‚Üî proj_fun I y = proj_fun I x :=
begin
  split,
  { intro h,
    exact proj_fun_unique _ _ _ h },
  { intro h,
    rw h,
    apply proj_fun_spec }
end

-- A description of the preimage of a set w.r.t. proj_fun
lemma proj_fun_preimage (I : X.clopen_cover) (S : set I) :
  proj_fun I ‚Åª¬π' S = ‚ãÉ (i : I) (hi : i ‚àà S), (i : set X) :=
begin
  rw [‚Üê S.bUnion_of_singleton, set.preimage_Union],
  congr' 1,
  ext1 U,
  rw [set.bUnion_of_singleton, set.preimage_Union],
  congr' 1,
  ext h x,
  split,
  { intro hx,
    simp only [set.mem_preimage, set.mem_singleton_iff] at hx,
    rw ‚Üê hx, apply proj_fun_spec, },
  { intro h,
    rw proj_fun_unique _ _ _ h,
    simp [proj_fun_spec] }
end

/-- A locally constant version of proj_fun -/
def proj (I : X.clopen_cover) : locally_constant X I :=
{ to_fun := proj_fun _,
  is_locally_constant := begin
    intros S,
    rw [proj_fun_preimage],
    apply is_open_bUnion,
    intros i hi,
    apply is_open,
  end}

lemma proj_map_comm {X Y : Profinite.{u}} {f : X ‚ü∂ Y}
  {I : X.clopen_cover} {J : Y.clopen_cover} (h : le_rel f I J) (x : X) :
  map h (I.proj x) = J.proj (f x) :=
begin
  apply proj_fun_unique,
  change x ‚àà f ‚Åª¬π' (map h (I.proj x)),
  apply map_spec,
  apply proj_fun_spec,
end

/-- A version of `I.proj` as a morphism in `Profinite`. -/
def œÄ (I : X.clopen_cover) : X ‚ü∂ Fintype_to_Profinite.obj (Fintype.of I) :=
{ to_fun := proj _,
  continuous_to_fun := locally_constant.continuous _ }

/-- This lemma shows the injectivity of the map `x ‚Ü¶ (proj I x)_I` -/
lemma eq_of_forall_proj_eq {x y : X} :
  (‚àÄ I : X.clopen_cover, proj I x = proj I y) ‚Üí x = y :=
begin
  intro h,
  suffices : x ‚àà ({y} : set X), by simpa using this,
  have : totally_disconnected_space X, by apply_instance,
  rw totally_disconnected_space_iff_connected_component_singleton at this,
  rw [‚Üê this, connected_component_eq_Inter_clopen],
  rintros U ‚ü®‚ü®U,hU1,hU2‚ü©,rfl‚ü©,
  dsimp,
  by_cases ht : U = ‚ä§, { rw ht, tauto },
  have : U·∂ú.nonempty, by rwa set.nonempty_compl,
  let J := of_clopen hU1 ‚ü®y,hU2‚ü© this,
  specialize h J,
  suffices : proj J y = of_clopen.mk,
  { change x ‚àà ((of_clopen.mk : J) : set X),
    rw [‚Üê this, ‚Üê h],
    apply proj_fun_spec },
  symmetry,
  apply proj_fun_unique,
  exact hU2,
end

/-- This lemma shows the surjectivity of the map from `X` to the limit of `I : X.clopen_cover`. -/
lemma exists_of_compat (Us : Œ† (I : X.clopen_cover), I)
  (compat : ‚àÄ {I J : X.clopen_cover} (h : I ‚â§ J), map h (Us I) = (Us J)) :
  ‚àÉ x : X, ‚àÄ I : X.clopen_cover, proj I x = Us I :=
begin
  have := is_compact.nonempty_Inter_of_directed_nonempty_compact_closed
    (Œª I, (Us I : set X)) (Œª I J, ‚ü®common I J, _‚ü©) (Œª I, I.nonempty _)
    (Œª I, is_closed.compact (is_closed _)) (Œª I, is_closed _),
  rcases this with ‚ü®x,hx‚ü©,
  { refine ‚ü®x,Œª I, _‚ü©,
    symmetry,
    apply proj_fun_unique,
    refine hx _ ‚ü®I,rfl‚ü© },
  dsimp only,
  rw [‚Üê compat (inf_le_left : I ‚äì J ‚â§ I), ‚Üê compat (inf_le_right : I ‚äì J ‚â§ J)],
  refine ‚ü®map_spec _ _, map_spec _ _‚ü©,
end

end proj

end clopen_cover

section limit_rep

variables (X : Profinite.{u})

/-- The diagram indexed by `X.clopen_cover` whose limit is isomorphic to `X`. -/
def diagram : X.clopen_cover ‚•§ Fintype.{u} :=
{ obj := Œª I, Fintype.of I,
  map := Œª I J h, clopen_cover.map $ le_of_hom h,
  map_id' := Œª I, by {ext1, erw [clopen_cover.map_id], simp },
  map_comp' := Œª I J K f g,
    by {ext1, simp only [Fintype.comp_apply], erw ‚Üê clopen_cover.map_comp, refl, } }

/-- The limit cone exhibiting `X` as a limit of `X.diagram`. -/
def Fincone : limits.cone (X.diagram ‚ãô Fintype_to_Profinite) :=
{ X := X,
  œÄ :=
  { app := Œª I, I.œÄ,
    naturality' := begin
      intros I J j,
      ext1 x,
      symmetry,
      apply clopen_cover.proj_fun_unique,
      simp only [Profinite.id_to_fun,
        id.def,
        category_theory.functor.comp_map,
        Profinite.comp_to_fun,
        function.comp_app,
        category_theory.functor.const.obj_map],
      apply clopen_cover.map_spec,
      apply clopen_cover.proj_fun_spec,
    end } }

instance is_iso_lift :
  is_iso ((limit_cone (X.diagram ‚ãô Fintype_to_Profinite)).is_limit.lift X.Fincone) :=
is_iso_of_bijective _
begin
  split,
  { intros x y h,
    apply clopen_cover.eq_of_forall_proj_eq,
    intros I,
    apply_fun (Œª u, u.val I) at h,
    exact h },
  { let C := (limit_cone (X.diagram ‚ãô Fintype_to_Profinite)).cone,
    rintros (x : C.X.to_Top),
    have := clopen_cover.exists_of_compat (Œª I : X.clopen_cover, x.val I) (Œª I J f, _),
    { rcases this with ‚ü®x,hx‚ü©,
      refine ‚ü®x,_‚ü©,
      ext1, ext1 I,
      exact hx I },
    { change _ = C.œÄ.app J _,
      erw ‚Üê C.w (hom_of_le f),
      refl } }
end

/-- 
The isomorphism of cones between `X.Fincone` and 
`limit_cone (X.diagram ‚ãô Fintype_to_Profinite)`. 
-/
def Fincone_iso : X.Fincone ‚âÖ (limit_cone _).cone :=
limits.cones.ext (as_iso $ (limit_cone _).is_limit.lift _) (Œª _, rfl)

/-- 
`X.Fincone` is indeed a limit cone. 
-/
def Fincone_is_limit : limits.is_limit X.Fincone :=
limits.is_limit.of_iso_limit (limit_cone_cone_is_limit _) X.Fincone_iso.symm

variables {X} {Y : Profinite.{u}}

/-- 
Change a cone over `Y.diagram ‚ãô Fintype_to_Profinite` 
with respect to a morphism `f : X ‚ü∂ Y`.
This is used to obtain the functorial properties of the `X.Fincone` constructions.
-/
def change_cone (f : Y ‚ü∂ X) (C : limits.cone (Y.diagram ‚ãô Fintype_to_Profinite)) :
  limits.cone (X.diagram ‚ãô Fintype_to_Profinite) :=
{ X := C.X,
  œÄ :=
  { app := Œª I, C.œÄ.app (clopen_cover.pullback f I) ‚â´
      ‚ü®clopen_cover.map (clopen_cover.pullback_le_rel _)‚ü©,
    naturality' := begin
      intros I J g,
      ext1,
      dsimp [diagram] at *,
      have h : clopen_cover.pullback f _ ‚â§ _ := clopen_cover.pullback_mono (le_of_hom g),
      erw [‚Üê C.w (hom_of_le h)],
      dsimp [Fintype_to_Profinite],
      simp_rw [‚Üê clopen_cover.map_comp],
      refl,
    end } }

theorem change_cone_lift (f : Y ‚ü∂ X) : f = X.Fincone_is_limit.lift (change_cone f Y.Fincone) :=
begin
  apply X.Fincone_is_limit.uniq (change_cone f Y.Fincone) f,
  intros I,
  ext1 y,
  change I.proj (f y) = _,
  dsimp [change_cone],
  symmetry,
  apply clopen_cover.proj_fun_unique,
  apply clopen_cover.map_spec,
  apply clopen_cover.proj_fun_spec,
end

/-- Changing a cone by an identity morphism results in a cone isomorphic to the given one. -/
def change_cone_id (C : limits.cone (X.diagram ‚ãô Fintype_to_Profinite)) :
  change_cone (ùüô X) C ‚âÖ C :=
limits.cones.ext (eq_to_iso rfl)
begin
  intros I,
  ext1,
  dsimp [change_cone] at *,
  symmetry,
  apply clopen_cover.map_unique,
  erw clopen_cover.pullback_id,
  simp,
end

/-- The compatibility of `change_cone` with respect to composition of morphisms. -/
def change_cone_comp {Z : Profinite.{u}} (g : Z ‚ü∂ Y) (f : Y ‚ü∂ X)
  (C : limits.cone (Z.diagram ‚ãô Fintype_to_Profinite)) :
  change_cone (g ‚â´ f) C ‚âÖ change_cone f (change_cone g C) :=
limits.cones.ext (eq_to_iso rfl)
begin
  intros I,
  ext1,
  dsimp [change_cone] at *,
  symmetry,
  apply clopen_cover.map_unique,
  rw clopen_cover.pullback_comp,
  refine le_trans (clopen_cover.map_spec (clopen_cover.pullback_le_rel _) _) _,
  nth_rewrite 1 set.preimage_comp,
  apply set.preimage_mono,
  apply clopen_cover.map_spec,
end

end limit_rep

namespace arrow

variable (f : arrow Profinite.{u})

/-- 
A gadget used to show that any arrow in `Profinite` can be expressed as a 
limit of arrows of `Fintype`s. 
This will be used as the category indexing the limit.
-/
@[nolint has_inhabited_instance]
structure index_cat : Type u :=
(left : f.left.clopen_cover)
(right : f.right.clopen_cover)
(compat : left.le_rel f.hom right)

namespace index_cat

variable {f}

/-- Morphisms for `index_cat`. -/
@[nolint has_inhabited_instance]
structure hom (A B : index_cat f) : Type u :=
(left : A.left ‚â§ B.left)
(right : A.right ‚â§ B.right)

instance : category (index_cat f) :=
{ hom := hom,
  id := Œª A, ‚ü®le_refl _, le_refl _‚ü©,
  comp := Œª A B C f g , ‚ü®le_trans f.left g.left, le_trans f.right g.right‚ü©,
  id_comp' := Œª A B f, by {cases f, refl},
  comp_id' := Œª A B f, by {cases f, refl},
  assoc' := Œª A B C D f g h, by {cases f, cases g, cases h, refl} }

/-- 
Make a term of `index_cat` given a clopen cover of a target of the arrow.
This is done fuunctorially.
-/
def mk_right : f.right.clopen_cover ‚•§ index_cat f :=
{ obj := Œª I,
  { left := clopen_cover.pullback f.hom I,
    right := I,
    compat := clopen_cover.pullback_le_rel _ },
  map := Œª I J f,
  { left := clopen_cover.pullback_mono $ le_of_hom f,
    right := le_of_hom f } }

/-- 
Make a term of `index_cat` given a clopen cover of a source of the arrow.
This is done fuunctorially.
-/
def mk_left : f.left.clopen_cover ‚•§ index_cat f :=
{ obj := Œª I,
  { left := I,
    right := ‚ä§,
    compat := clopen_cover.le_rel_top _ _ },
  map := Œª I J f,
  { left := le_of_hom f,
    right := clopen_cover.le_rel_top _ _ } }

/-- 
A combination of `mk_left` and `mk_right`.
-/
def make : f.left.clopen_cover ‚•§ f.right.clopen_cover ‚•§ index_cat f :=
{ obj := Œª I,
  { obj := Œª J,
    { left := I ‚äì clopen_cover.pullback f.hom J,
      right := J,
      compat := begin
        dsimp,
        have : f.hom = ùüô _ ‚â´ f.hom, by simp,
        rw this,
        refine clopen_cover.le_rel_comp _ (clopen_cover.pullback_le_rel _),
        simp only [category_theory.category.id_comp],
        dsimp,
        exact (inf_le_right : I ‚äì clopen_cover.pullback f.hom J ‚â§ _)
      end },
    map := Œª I J g,
    { left := clopen_cover.inf_mono_right $ clopen_cover.pullback_mono $ le_of_hom g,
      right := le_of_hom g } },
  map := Œª I J g,
  { app := Œª K,
    { left := clopen_cover.inf_mono_left $ le_of_hom g,
      right := le_refl _ } } }.

end index_cat

/-- 
The diagram whose limit is a given arrow in `Profinite`.
-/
def diagram : index_cat f ‚•§ arrow Fintype.{u} :=
{ obj := Œª A,
  { left := Fintype.of A.left,
    right := Fintype.of A.right,
    hom := clopen_cover.map A.compat },
  map := Œª A B g,
  { left := clopen_cover.map g.left,
    right := clopen_cover.map g.right,
    w' := begin
      ext1 x,
      dsimp,
      simp_rw ‚Üê clopen_cover.map_comp,
      refl,
    end },
  map_id' := begin
    intros A,
    ext1,
    all_goals {
      dsimp,
      ext1,
      erw clopen_cover.map_id,
      refl },
  end,
  map_comp' := begin
    intros A B C f g,
    ext1,
    all_goals {
      ext1,
      dsimp,
      erw ‚Üê clopen_cover.map_comp,
      refl },
  end }

/-- An abbreviation for `diagram f ‚ãô Fintype_to_Profinite.map_arrow`. -/
abbreviation diagram' : index_cat f ‚•§ arrow Profinite := diagram f ‚ãô Fintype_to_Profinite.map_arrow

/-- The diagram of profinite sets obtained from the sources of `diagram'`. -/
abbreviation left_diagram : index_cat f ‚•§ Profinite := diagram' f ‚ãô arrow.left_func

/-- The diagram of profinite sets obtained from the targets of `diagram'`. -/
abbreviation right_diagram : index_cat f ‚•§ Profinite := diagram' f ‚ãô arrow.right_func

/-- The usual limit cone over `diagram' f`. -/
def limit_cone : limits.limit_cone (diagram' f) :=
arrow.limit_cone _ (limit_cone $ left_diagram _) (limit_cone $ right_diagram _)

/-- 
The cone which we want to show is a limit cone of `diagram' f`.
Its cone point is the given arrow `f`.
-/
def Fincone : limits.cone (diagram' f) :=
{ X := f,
  œÄ :=
  { app := Œª Is,
    { left := clopen_cover.œÄ _,
      right := clopen_cover.œÄ _,
      w' := begin
        ext1 x,
        dsimp [diagram, clopen_cover.œÄ, Fintype_to_Profinite],
        erw clopen_cover.proj_map_comm,
      end },
    naturality' := begin
      intros Is Js f,
      ext1;
      ext1 x,
      { dsimp [clopen_cover.œÄ, diagram, Fintype_to_Profinite],
        erw clopen_cover.proj_map_comm,
        refl },
      { dsimp [clopen_cover.œÄ, diagram, Fintype_to_Profinite],
        erw clopen_cover.proj_map_comm,
        refl }
    end } }.

instance is_iso_lift_left : is_iso ((limit_cone f).is_limit.lift (Fincone f)).left :=
is_iso_of_bijective _
begin
  split,
  { intros x y h,
    apply clopen_cover.eq_of_forall_proj_eq,
    intros I,
    apply_fun subtype.val at h,
    let II := index_cat.mk_left.obj I,
    apply_fun (Œª f, f II) at h,
    exact h },
 { intros x,
    cases x with x hx,
    dsimp at *,
    let Us : Œ† (I : f.left.clopen_cover), I := Œª U, x (index_cat.mk_left.obj U),
    rcases clopen_cover.exists_of_compat Us _ with ‚ü®y,hy‚ü©,
    { refine ‚ü®y,_‚ü©,
      ext Is : 2,
      dsimp at *,
      change clopen_cover.proj _ _ = _,
      have : x Is = Us Is.left,
      { let ff : Is ‚ü∂ index_cat.mk_left.obj Is.left := ‚ü®le_refl _,clopen_cover.le_rel_top _ _‚ü©,
        dsimp [Us],
        rw ‚Üê hx ff,
        apply clopen_cover.map_unique,
        simp },
      rw this,
      apply hy },
    { intros I J h,
      specialize hx (index_cat.mk_left.map $ hom_of_le h),
      exact hx } }
end

instance is_iso_lift_right : is_iso ((limit_cone f).is_limit.lift (Fincone f)).right :=
is_iso_of_bijective _
begin
  split,
  { intros x y h,
    apply clopen_cover.eq_of_forall_proj_eq,
    intros I,
    apply_fun subtype.val at h,
    let II := index_cat.mk_right.obj I,
    apply_fun (Œª f, f II) at h,
    change clopen_cover.proj _ _ = clopen_cover.proj _ _ at h,
    have hII : II.right ‚â§ I := le_refl _,
    erw [‚Üê clopen_cover.proj_map_comm hII, h, clopen_cover.proj_map_comm],
    simp },
  { intros x,
    cases x with x hx,
    let Us : Œ† (I : f.right.clopen_cover), I := Œª U, x (index_cat.mk_right.obj U),
    rcases clopen_cover.exists_of_compat Us _ with ‚ü®y,hy‚ü©,
    { refine ‚ü®y,_‚ü©,
      ext Is : 2,
      dsimp at *,
      change clopen_cover.proj _ _ = _,
      have : x Is = Us Is.right,
      { let ff : Is ‚ü∂ index_cat.mk_right.obj Is.right := ‚ü®_,le_refl _‚ü©,
        dsimp [Us],
        rw ‚Üê hx ff,
        apply clopen_cover.map_unique,
        simp only [set.preimage_id, Profinite.id_to_fun, set.le_eq_subset],
        dsimp [index_cat.mk_right],
        intros U,
        rcases Is.compat U with ‚ü®V,hV‚ü©,
        refine ‚ü®‚ü®_,_,V,rfl‚ü©,_‚ü©,
        rcases Is.left.nonempty U with ‚ü®z,hz‚ü©,
        refine ‚ü®z, hV hz‚ü©,
        simpa },
      rw this,
      apply hy },
    { intros I J h,
      specialize hx (index_cat.mk_right.map $ hom_of_le h),
      exact hx } }
end

-- sanity check
example : is_iso ((limit_cone f).is_limit.lift (Fincone f)) := by apply_instance

/-- The isomorphism between `Fincone f` and the cone of the limit cone `(limit_cone f)`. -/
def Fincone_iso : Fincone f ‚âÖ (limit_cone f).cone :=
limits.cones.ext (as_iso ((limit_cone f).is_limit.lift (Fincone f))) (Œª I, rfl)

/-- `Fincone f` is indeed a limit cone. -/
def Fincone_is_limit : limits.is_limit (Fincone f) :=
limits.is_limit.of_iso_limit (limit_cone f).is_limit (Fincone_iso f).symm

/--
If `f` is surjective, then the terms in the diagram whose limit is `f` are all surjective as well.
-/
lemma surjective_of_surjective (surj : function.surjective f.hom) (I : index_cat f) :
  function.surjective ((diagram f).obj I).hom :=
begin
  intros U,
  change ‚Ü•I.right at U,
  rcases I.right.nonempty U with ‚ü®x,hx‚ü©,
  rcases surj x with ‚ü®y,rfl‚ü©,
  let V : ‚Ü•(clopen_cover.pullback f.hom I.right) :=
    ‚ü®f.hom ‚Åª¬π' (U : set f.right),‚ü®y,hx‚ü©,_,rfl‚ü©,
  rcases clopen_cover.nonempty _ V with ‚ü®z,hz‚ü©,
  use clopen_cover.proj _ z,
  dsimp [diagram],
  erw clopen_cover.proj_map_comm,
  symmetry,
  apply clopen_cover.proj_fun_unique,
  exact hz,
end

end arrow

end Profinite
