import category_theory.derived
import data.matrix.notation

import for_mathlib.homological_complex
import for_mathlib.horseshoe
import for_mathlib.split_exact

noncomputable theory

open category_theory
open category_theory.limits
open short_exact_sequence

universes w v u

namespace category_theory

variables {C : Type u} [category.{v} C] {D : Type*} [category D]

-- Importing `category_theory.abelian.projective` and assuming
-- `[abelian C] [enough_projectives C] [abelian D]` suffices to acquire all the following:
-- variables [preadditive C] [has_zero_object C] [has_equalizers C]
-- variables [has_images C] [has_projective_resolutions C]
-- variables [preadditive D] [has_zero_object D] [has_equalizers D] [has_cokernels D]
-- variables [has_images D] [has_image_maps D]

variables [abelian C] [enough_projectives C] [abelian D]

namespace functor
namespace left_derived

variables (F : C ‚•§ D)

/-- We can compute a left derived functor using a chosen projective resolution. -/
@[simps]
def functor.left_derived_obj_iso' (F : C ‚•§ D) [F.additive] (n : ‚Ñï)
  (X : C) (P : chain_complex C ‚Ñï) (œÄ : P ‚ü∂ ((chain_complex.single‚ÇÄ C).obj X))
  (h : P.is_projective_resolution X œÄ) :
  (F.left_derived n).obj X ‚âÖ
    (homology_functor D _ n).obj ((F.map_homological_complex _).obj P) :=
(F.left_derived_obj_iso n (h.mk_ProjectiveResolution P X œÄ) : _)

/-- We can compute a left derived functor on a morphism using a lift of that morphism to a chain map
between chosen projective resolutions. -/
lemma functor.left_derived_map_eq' (F : C ‚•§ D) [F.additive] (n : ‚Ñï) (X Y : C) (f : X ‚ü∂ Y)
  (PX : chain_complex C ‚Ñï) (œÄX : PX ‚ü∂ ((chain_complex.single‚ÇÄ C).obj X))
  (PY : chain_complex C ‚Ñï) (œÄY : PY ‚ü∂ ((chain_complex.single‚ÇÄ C).obj Y)) (g : PX ‚ü∂ PY)
  (hX : PX.is_projective_resolution X œÄX) (hY : PY.is_projective_resolution Y œÄY)
  (w : g ‚â´ œÄY = œÄX ‚â´ (chain_complex.single‚ÇÄ C).map f) :
  (F.left_derived n).map f =
  (functor.left_derived_obj_iso' F n X PX œÄX hX).hom ‚â´
    (homology_functor D _ n).map ((F.map_homological_complex _).map g) ‚â´
    (functor.left_derived_obj_iso' F n Y PY œÄY hY).inv :=
begin
  let PXr := (hX.mk_ProjectiveResolution PX X œÄX),
  let PYr := (hY.mk_ProjectiveResolution PY Y œÄY),
  let gr : PXr.complex ‚ü∂ PYr.complex := g,
  simpa using functor.left_derived_map_eq F n f gr w,
end
.

def Œ¥ [F.additive] (n : ‚Ñï) (A : short_exact_sequence C) :
  (F.left_derived (n+1)).obj A.3 ‚ü∂ (F.left_derived n).obj A.1 :=
begin
  let f‚ÇÉ := functor.left_derived_obj_iso' F (n+1) _ _ _ (horseshoe_is_projective_resolution‚ÇÉ A),
  let f‚ÇÅ := functor.left_derived_obj_iso' F n _ _ _ (horseshoe_is_projective_resolution‚ÇÅ A),
  refine f‚ÇÉ.hom ‚â´ _ ‚â´ f‚ÇÅ.symm.hom,
  apply homological_complex.Œ¥ n (map_complex_short_exact_sequence_of_split C F _
    (Œª i, horseshoe_split A i)),
end

lemma exact_of_short_exact [F.additive] (n : ‚Ñï) (A : short_exact_sequence C) :
  exact ((F.left_derived n).map A.f) ((F.left_derived n).map A.g) :=
begin
  let P := map_complex_short_exact_sequence_of_split C F _ (Œª i, horseshoe_split A i),
  have := ((homological_complex.six_term_exact_seq n P).drop 3).pair,
  have H‚ÇÅ‚ÇÇ := functor.left_derived_map_eq' F n A.1 A.2 A.f
    ((homological_complex.Fst C).obj (horseshoe A)) (horseshoe_to_single‚ÇÅ A)
    ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
    ((homological_complex.Fst_Snd C).app (horseshoe A))
    (horseshoe_is_projective_resolution‚ÇÅ A)
    (horseshoe_is_projective_resolution‚ÇÇ A) _,
  have H‚ÇÇ‚ÇÉ := functor.left_derived_map_eq' F n A.2 A.3 A.g
    ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
    ((homological_complex.Trd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÉ A)
    ((homological_complex.Snd_Trd C).app (horseshoe A))
    (horseshoe_is_projective_resolution‚ÇÇ A)
    (horseshoe_is_projective_resolution‚ÇÉ A) _,
  refine preadditive.exact_of_iso_of_exact' _ _ _ _ _ _ _ _ _ this,
  { let := functor.left_derived_obj_iso' F n A.1
      ((homological_complex.Fst C).obj (horseshoe A)) (horseshoe_to_single‚ÇÅ A)
      (horseshoe_is_projective_resolution‚ÇÅ A),
    exact this.symm },
  { let := functor.left_derived_obj_iso' F n A.2
      ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
      (horseshoe_is_projective_resolution‚ÇÇ A),
    exact this.symm },
  { let := functor.left_derived_obj_iso' F n A.3
      ((homological_complex.Trd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÉ A)
      (horseshoe_is_projective_resolution‚ÇÉ A),
    exact this.symm },
  { rw [H‚ÇÅ‚ÇÇ, ‚Üê category.assoc, iso.symm_hom, iso.inv_hom_id, category.id_comp],
    simpa },
  { rw [H‚ÇÇ‚ÇÉ, ‚Üê category.assoc, iso.symm_hom, iso.inv_hom_id, category.id_comp],
    simpa },
  { ext i,
    apply horseshoe_g_comp_to_single‚ÇÉ_f, },
  { ext i,
    apply horseshoe_f_comp_to_single‚ÇÇ_f }
end

lemma exact_of_short_exact.Œ¥_right [F.additive] (n : ‚Ñï) (A : short_exact_sequence C) :
  exact ((F.left_derived (n + 1)).map A.g) (Œ¥ F n A) :=
begin
  let P := map_complex_short_exact_sequence_of_split C F _ (Œª i, horseshoe_split A i),
  have := ((homological_complex.six_term_exact_seq n P).drop 1).pair,
  have H‚ÇÇ‚ÇÉ := functor.left_derived_map_eq' F (n+1) A.2 A.3 A.g
    ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
    ((homological_complex.Trd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÉ A)
    ((homological_complex.Snd_Trd C).app (horseshoe A))
    (horseshoe_is_projective_resolution‚ÇÇ A)
    (horseshoe_is_projective_resolution‚ÇÉ A) _,
  refine preadditive.exact_of_iso_of_exact' _ _ _ _ _ _ _ _ _ this,
  { let := functor.left_derived_obj_iso' F (n+1) A.2
      ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
      (horseshoe_is_projective_resolution‚ÇÇ A),
    exact this.symm },
  { let := functor.left_derived_obj_iso' F (n+1) A.3
      ((homological_complex.Trd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÉ A)
      (horseshoe_is_projective_resolution‚ÇÉ A),
    exact this.symm },
  { let := functor.left_derived_obj_iso' F n A.1
      ((homological_complex.Fst C).obj (horseshoe A)) (horseshoe_to_single‚ÇÅ A)
      (horseshoe_is_projective_resolution‚ÇÅ A),
    exact this.symm },
  { rw [H‚ÇÇ‚ÇÉ, ‚Üê category.assoc, iso.symm_hom, iso.inv_hom_id, category.id_comp],
    simpa },
  { unfold Œ¥,
    dsimp,
    simp only [category.assoc, iso.inv_hom_id_assoc], },
  { ext i,
    apply horseshoe_g_comp_to_single‚ÇÉ_f }
end

lemma exact_of_short_exact.Œ¥_left [F.additive] (n : ‚Ñï) (A : short_exact_sequence C) :
  exact (Œ¥ F n A) ((F.left_derived n).map A.f) :=
begin
  let P := map_complex_short_exact_sequence_of_split C F _ (Œª i, horseshoe_split A i),
  have := ((homological_complex.six_term_exact_seq n P).drop 2).pair,
  have H‚ÇÅ‚ÇÇ := functor.left_derived_map_eq' F n A.1 A.2 A.f
    ((homological_complex.Fst C).obj (horseshoe A)) (horseshoe_to_single‚ÇÅ A)
    ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
    ((homological_complex.Fst_Snd C).app (horseshoe A))
    (horseshoe_is_projective_resolution‚ÇÅ A)
    (horseshoe_is_projective_resolution‚ÇÇ A) _,
  refine preadditive.exact_of_iso_of_exact' _ _ _ _ _ _ _ _ _ this,
  { let := functor.left_derived_obj_iso' F (n+1) A.3
      ((homological_complex.Trd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÉ A)
      (horseshoe_is_projective_resolution‚ÇÉ A),
    exact this.symm },
  { let := functor.left_derived_obj_iso' F n A.1
      ((homological_complex.Fst C).obj (horseshoe A)) (horseshoe_to_single‚ÇÅ A)
      (horseshoe_is_projective_resolution‚ÇÅ A),
    exact this.symm },
  { let := functor.left_derived_obj_iso' F n A.2
      ((homological_complex.Snd C).obj (horseshoe A)) (horseshoe_to_single‚ÇÇ A)
      (horseshoe_is_projective_resolution‚ÇÇ A),
    exact this.symm },
  { unfold Œ¥,
    dsimp,
    simp only [category.assoc, iso.inv_hom_id_assoc], },
  { rw [H‚ÇÅ‚ÇÇ, ‚Üê category.assoc, iso.symm_hom, iso.inv_hom_id, category.id_comp],
    simpa },
  { ext i,
    apply horseshoe_f_comp_to_single‚ÇÇ_f }
end

lemma six_term_exact_seq [F.additive] (n : ‚Ñï) (A : short_exact_sequence C) :
  exact_seq D [
    (F.left_derived (n+1)).map A.f, (F.left_derived (n+1)).map A.g,
    Œ¥ F n A,
    (F.left_derived n).map A.f, (F.left_derived n).map A.g] :=
begin
  refine exact_seq.cons _ _ (exact_of_short_exact _ _ _) _ _,
  refine exact_seq.cons _ _ (exact_of_short_exact.Œ¥_right _ _ _) _ _,
  refine exact_seq.cons _ _ (exact_of_short_exact.Œ¥_left _ _ _) _ _,
  refine exact_seq.cons _ _ (exact_of_short_exact _ _ _) _ _,
  apply exact_seq.single,
end

end left_derived
end functor
end category_theory

section right_exact

namespace category_theory

open category_theory.functor

variables {C : Type u} {D : Type v} [category.{w} C] [category.{w} D] [abelian C] [abelian D]
variables (F : C ‚•§ D) [additive F] {X : C}
variables [limits.preserves_finite_colimits F]

/-- The morphism `cokernel (kernel.lift (0 : Y ‚ü∂ Z) f) ‚ü∂ cokernel f`. -/
@[simp] def cokernel_lift_to_cokernel {X Y Z : C} (f : X ‚ü∂ Y) :
  cokernel (kernel.lift (0 : Y ‚ü∂ Z) f (by simp)) ‚ü∂ cokernel f :=
cokernel.desc _ ((kernel.Œπ 0) ‚â´ cokernel.œÄ _) (by simp)

/-- The morphism `cokernel f ‚ü∂ cokernel (kernel.lift (0 : Y ‚ü∂ Z) f)`. -/
@[simp] def cokernel_to_cokernel_lift {X Y Z : C} (f : X ‚ü∂ Y) :
  cokernel f ‚ü∂ cokernel (kernel.lift (0 : Y ‚ü∂ Z) f (by simp)) :=
cokernel.map _ _ (ùüô _) (kernel.lift _ (ùüô _) (by simp)) (by { ext, simp })

/-- The isomorphism `cokernel f ‚âÖ cokernel (kernel.lift (0 : Y ‚ü∂ Z) f)`. -/
def cokernel_lift_iso_cokernel {X Y Z : C} (f : X ‚ü∂ Y) :
  cokernel (kernel.lift (0 : Y ‚ü∂ Z) f (by simp)) ‚âÖ cokernel f :=
{ hom := cokernel_lift_to_cokernel f,
  inv := cokernel_to_cokernel_lift f,
  hom_inv_id' :=
  begin
    ext,
    simp only [cokernel_lift_to_cokernel, cokernel_to_cokernel_lift, coequalizer_as_cokernel,
      cokernel.œÄ_desc_assoc, category.assoc, cokernel.œÄ_desc, category.comp_id],
    rw [‚Üê kernel_zero_iso_source_hom, ‚Üê kernel_zero_iso_source_inv, ‚Üê category.assoc,
      iso.hom_inv_id, category.id_comp],
  end,
  inv_hom_id' := by { ext, simp } }

/-- The isomorphism `cokernel f ‚ü∂ homology f (0 : Y ‚ü∂ Z)`. -/
def cokernel_homology_iso {X Y Z : C} (f : X ‚ü∂ Y) :
  homology f (0 : Y ‚ü∂ Z) (by simp) ‚âÖ cokernel f :=
homology_iso_cokernel_lift _ _ _ ‚â™‚â´ cokernel_lift_iso_cokernel f

lemma short_exact_of_resolution (P: ProjectiveResolution X) : exact_seq C
  [P.complex.d 1 0, P.œÄ.f 0, (0 : X ‚ü∂ X)] :=
begin
  refine exact_seq.cons _ _ P.exact‚ÇÄ _ _,
  rw ‚Üê exact_iff_exact_seq,
  exact ((abelian.tfae_epi X (P.œÄ.f 0)).out 0 2).1 P.epi
end

lemma short_exact_of_resolution_functor (P: ProjectiveResolution X) : exact_seq D
  [((F.map_homological_complex (complex_shape.down ‚Ñï)).obj P.complex).d_to 0,
  F.map (P.œÄ.f 0), (0 : F.obj X ‚ü∂ F.obj X)] :=
begin
  refine exact_seq.cons _ _ _ _ _,
  { have : (complex_shape.down ‚Ñï).rel 1 0 := rfl,
    let f := (homological_complex.X_prev_iso ((F.map_homological_complex _).obj P.complex) this),
    simp at this,
    refine preadditive.exact_of_iso_of_exact' (F.map (P.complex.d 1 0)) (F.map (P.œÄ.f 0)) _ _
      f.symm (iso.refl _) (iso.refl _) (by simp) (by simp) _,
    exact (exact_iff_exact_seq _ _ ).2
      ((right_exact.preserves_exact_seq F (short_exact_of_resolution P)).extract 0 2) },
  rw ‚Üê exact_iff_exact_seq,
  refine ((abelian.tfae_epi (F.obj X) (F.map (P.œÄ.f 0))).out 0 2).1
    (category_theory.preserves_epi F _),
end

/-- The iso `(F.left_derived 0).obj X ‚âÖ F.obj X`. -/
def functor.left_derived.zero_iso [enough_projectives C] : (F.left_derived 0).obj X ‚âÖ F.obj X :=
begin
  let P := ProjectiveResolution.of X,
  refine (left_derived_obj_iso F 0 P) ‚â™‚â´ (_ ‚â™‚â´ (as_iso $ right_exact.cokernel_comparison
    $ short_exact_of_resolution_functor F P)),
  show homology _ _ _ ‚âÖ _,
  convert cokernel_homology_iso _,
  simp
end

end category_theory

end right_exact
