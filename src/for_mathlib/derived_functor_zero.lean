import category_theory.functor.category

import for_mathlib.homology
import for_mathlib.derived_functor


universes w v u

noncomputable theory

namespace category_theory

open functor functor.left_derived

variables {C : Type u} {D : Type v} [category.{w} C] [category.{w} D]
variables (F : C ‚•§ D) {A‚ÇÅ A‚ÇÇ A‚ÇÉ X : C} {f : A‚ÇÅ ‚ü∂ A‚ÇÇ} {g : A‚ÇÇ ‚ü∂ A‚ÇÉ}
variables [abelian C] [abelian D] [additive F]

namespace limits

/-- The iso `parallel_pair f 0 ‚ãô F ‚âÖ parallel_pair (F.map f) 0`. -/
def cokernel_diagram_iso {A B : C} (f : A ‚ü∂ B) :
  parallel_pair f 0 ‚ãô F ‚âÖ parallel_pair (F.map f) 0 :=
nat_iso.of_components (Œª X,
  match X with
  | walking_parallel_pair.zero := iso.refl _
  | walking_parallel_pair.one := iso.refl _
  end)
begin
  rintros (a|a) (b|b) (f|f),
  work_on_goal 0 { dsimp at *, simp at *, dsimp at *, simp at * },
  work_on_goal 0 { dsimp at *, unfold_aux, dsimp at *, simp at * },
  work_on_goal 0 { dsimp at *, simp at * },
  dsimp at *, simp at *, dsimp at *, simp at *,
end

/-- A morphism `cokernel f ‚ü∂ A‚ÇÉ` provided that `f ‚â´ g = 0`. -/
def cokernel_comparison (w : f ‚â´ g = 0) : cokernel f ‚ü∂ A‚ÇÉ :=
cokernel.desc f g w

end limits

namespace functor.right_exact

open limits

/-- The iso `F.obj (cokernel f) ‚âÖ cokernel (F.map f)` if `preserves_finite_colimits F`. -/
def preserves_cokernel [preserves_finite_colimits F] {A B : C} (f : A ‚ü∂ B) :
  F.obj (cokernel f) ‚âÖ cokernel (F.map f) :=
(is_colimit_of_preserves _ (colimit.is_colimit _)).cocone_point_unique_up_to_iso
  (colimit.is_colimit _) ‚â™‚â´ limits.has_colimit.iso_of_nat_iso
  (cokernel_diagram_iso _ _)

@[simp, reassoc]
lemma map_preserves_cokernel_hom [limits.preserves_finite_colimits F] :
  F.map (cokernel.œÄ f) ‚â´ (preserves_cokernel F f).hom = cokernel.œÄ (F.map f) :=
begin
  erw (is_colimit_of_preserves F (colimit.is_colimit (parallel_pair f 0))).fac_assoc,
  dsimp, simp only [has_colimit.iso_of_nat_iso_Œπ_hom],
  dsimp [cokernel_diagram_iso],
  simp,
end

variable {F}

-- Do we have some API with `exact_seq` to get lemmas like this?
lemma comp_eq_zero (ex : exact_seq C [f, g, (0 : A‚ÇÉ ‚ü∂ X)]) : f ‚â´ g = 0 :=
begin
  suffices : exact f g, by exact this.1,
  rw exact_iff_exact_seq,
  exact ex.extract 0 2,
end

variable (F)

lemma map_comp_eq_zero (ex : exact_seq C [f, g, (0 : A‚ÇÉ ‚ü∂ X)]) : F.map f ‚â´ F.map g = 0 :=
by { rw [‚Üê F.map_comp, comp_eq_zero ex], simp }

variable {F}

local attribute [instance] abelian.pseudoelement.over_to_sort
  abelian.pseudoelement.hom_to_fun
  abelian.pseudoelement.has_zero

instance comparison_is_iso_of_exact (ex : exact_seq C [f, g, (0 : A‚ÇÉ ‚ü∂ X)]) :
  is_iso (cokernel_comparison (comp_eq_zero ex)) :=
begin
  letI : epi g := ((abelian.tfae_epi X g).out 0 2).2 ((exact_iff_exact_seq _ _).2 $ ex.extract 1 2),
  refine (is_iso_iff_mono_and_epi _).2 ‚ü®_, limits.cokernel.desc_epi _ _ _‚ü©,
  refine abelian.pseudoelement.mono_of_zero_of_map_zero _ (Œª a ha, _),
  obtain ‚ü®b, hb‚ü© := abelian.pseudoelement.pseudo_surjective_of_epi (cokernel.œÄ f) a,
  have hbz : g b = 0,
  { have : g = (cokernel.œÄ f) ‚â´ (cokernel_comparison (comp_eq_zero ex)) :=
      (cokernel.œÄ_desc _ _ _).symm,
    rw [this, abelian.pseudoelement.comp_apply, hb, ha] },
  obtain ‚ü®c, hc : f c = b‚ü© := abelian.pseudoelement.pseudo_exact_of_exact.2 _ hbz,
  { rw [‚Üê hc, ‚Üê abelian.pseudoelement.comp_apply, cokernel.condition,
      abelian.pseudoelement.zero_apply] at hb,
    exact hb.symm },
  { exact (exact_iff_exact_seq _ _).2 (ex.extract 0 2) }
end

@[simp, reassoc]
lemma cokernel_comparison_inv (ex : exact_seq C [f, g, (0 : A‚ÇÉ ‚ü∂ X)]) :
  g ‚â´ inv (cokernel_comparison (comp_eq_zero ex)) = cokernel.œÄ _ :=
begin
  rw is_iso.comp_inv_eq,
  dsimp [cokernel_comparison],
  simp,
end

lemma aux [limits.preserves_finite_colimits F] (ex : exact_seq C [f, g, (0 : A‚ÇÉ ‚ü∂ X)]) :
  F.map g ‚â´ (F.map $ inv (cokernel_comparison (comp_eq_zero ex))) ‚â´ (preserves_cokernel _ _).hom =
  cokernel.œÄ (F.map f) :=
by simp only [‚Üê category.assoc, ‚Üê F.map_comp, cokernel_comparison_inv, map_preserves_cokernel_hom]

variable (F)

lemma preserves_exact_seq [limits.preserves_finite_colimits F]
  (ex : exact_seq C [f, g, (0 : A‚ÇÉ ‚ü∂ X)]) :
  exact_seq D [F.map f, F.map g, (0 : F.obj A‚ÇÉ ‚ü∂ F.obj X)] :=
begin
  have exact := (exact_iff_exact_seq _ _).2 (ex.extract 0 2),
  haveI epi : epi g,
  { replace ex : exact_seq C ([g, _]) := ex.extract 1 2,
    rwa [‚Üê exact_iff_exact_seq, ‚Üê (abelian.tfae_epi X g).out 0 2] at ex },
  refine exact_seq.cons _ _ _ _ _,
  { let I : F.obj A‚ÇÉ ‚âÖ cokernel (F.map f) :=
      (F.map_iso $ (as_iso $ cokernel_comparison (comp_eq_zero ex)).symm) ‚â™‚â´ preserves_cokernel _ _,
    suffices : category_theory.exact (F.map f) (F.map g ‚â´ I.hom), by rwa exact_comp_iso at this,
    erw aux,
    exact abelian.exact_cokernel (F.map f) },
  rw [‚Üê exact_iff_exact_seq, ‚Üê (abelian.tfae_epi (F.obj X) (F.map g)).out 0 2],
  exact category_theory.preserves_epi _ _,
end

lemma short_exact_of_resolution (P: ProjectiveResolution X) : exact_seq C
  [P.complex.d 1 0, P.œÄ.f 0, (0 : X ‚ü∂ X)] :=
begin
  refine exact_seq.cons _ _ P.exact‚ÇÄ _ _,
  rw ‚Üê exact_iff_exact_seq,
  exact ((abelian.tfae_epi X (P.œÄ.f 0)).out 0 2).1 P.epi
end

lemma short_exact_of_resolution_functor (P: ProjectiveResolution X)
  [limits.preserves_finite_colimits F] : exact_seq D
  [((F.map_homological_complex (complex_shape.down ‚Ñï)).obj P.complex).d_to 0,
  F.map (P.œÄ.f 0), (0 : F.obj X ‚ü∂ F.obj X)] :=
begin
  refine exact_seq.cons _ _ _ _ _,
  { have : (complex_shape.down ‚Ñï).rel 1 0 := rfl,
    let f := (homological_complex.X_prev_iso ((F.map_homological_complex _).obj P.complex) this),
    refine preadditive.exact_of_iso_of_exact' (F.map (P.complex.d 1 0)) (F.map (P.œÄ.f 0)) _ _
      f.symm (iso.refl _) (iso.refl _) (by simp) (by simp) _,
    exact (exact_iff_exact_seq _ _ ).2
      ((preserves_exact_seq F (short_exact_of_resolution P)).extract 0 2) },
  rw ‚Üê exact_iff_exact_seq,
  refine ((abelian.tfae_epi (F.obj X) (F.map (P.œÄ.f 0))).out 0 2).1
    (category_theory.preserves_epi F _),
end

/-- Given `P : ProjectiveResolution X`, a morphism `(F.left_derived 0).obj X ‚ü∂ F.obj X`. -/
@[nolint unused_arguments]
def left_derived.zero_to_self_app [enough_projectives C] {X : C}
  (P : ProjectiveResolution X) : (F.left_derived 0).obj X ‚ü∂ F.obj X :=
(left_derived_obj_iso F 0 P).hom ‚â´ homology.desc' _ _ _ (kernel.Œπ _ ‚â´ (F.map (P.œÄ.f 0)))
begin
  { have : (complex_shape.down ‚Ñï).rel 1 0 := rfl,
    rw [kernel.lift_Œπ_assoc, homological_complex.d_to_eq _ this, map_homological_complex_obj_d,
      category.assoc, ‚Üê functor.map_comp],
    simp },
end
‚â´ F.map (ùüô _)

/-- Given `P : ProjectiveResolution X`, a morphism `F.obj X ‚ü∂ (F.left_derived 0).obj X` given
`preserves_finite_colimits F`. -/
@[nolint unused_arguments]
def left_derived.zero_to_self_app_inv [enough_projectives C] [preserves_finite_colimits F] {X : C}
  (P : ProjectiveResolution X) : F.obj X ‚ü∂ (F.left_derived 0).obj X :=
begin
  refine ((@as_iso _ _ _ _ _ (category_theory.functor.right_exact.comparison_is_iso_of_exact
    (short_exact_of_resolution_functor F P))).inv) ‚â´ _ ‚â´ (homology_iso_cokernel_lift _ _ _).inv ‚â´
    (left_derived_obj_iso F 0 P).inv,
  exact cokernel.map _ _ (ùüô _) (kernel.lift _ (ùüô _) (by simp)) (by { ext, simp }),
end

lemma left_derived.zero_to_self_app_comp_inv [enough_projectives C] [preserves_finite_colimits F]
  {X : C} (P : ProjectiveResolution X) : left_derived.zero_to_self_app F P ‚â´
  left_derived.zero_to_self_app_inv F P = ùüô _ :=
begin
  dsimp [left_derived.zero_to_self_app, left_derived.zero_to_self_app_inv],
  rw [functor.map_id, category.comp_id, category.assoc],
  refine (iso.eq_inv_comp _).1 _,
  rw [‚Üê category.assoc, ‚Üê category.assoc, ‚Üê category.assoc],
  refine (iso.comp_inv_eq _).2 _,
  rw [category.comp_id, iso.inv_hom_id, iso.comp_inv_eq, category.id_comp],
  ext,
  simp only [category.assoc, homology.desc'_œÄ'_assoc, cokernel_comparison_inv_assoc,
    cokernel.œÄ_desc, homology.œÄ', iso.inv_hom_id, category.comp_id],
  nth_rewrite 1 [‚Üê category.comp_id (cokernel.œÄ _)],
  refine congr_arg (category_struct.comp _) _,
  dsimp [homology.desc'],
  rw [‚Üê category.assoc, ‚Üê category.assoc, ‚Üê category.assoc, iso.inv_hom_id, category.id_comp],
  ext,
  simp only [coequalizer_as_cokernel, category.assoc, cokernel.œÄ_desc_assoc,
    cokernel_comparison_inv_assoc, cokernel.œÄ_desc, category.comp_id],
  rw [‚Üê category.assoc],
  nth_rewrite 1 [‚Üê category.id_comp (cokernel.œÄ _)],
  refine congr_fun (congr_arg category_struct.comp _) _,
  ext,
  simp only [category.assoc, kernel.lift_Œπ, category.comp_id, category.id_comp],
end

lemma left_derived.zero_to_self_app_inv_comp [enough_projectives C] [preserves_finite_colimits F]
  {X : C} (P : ProjectiveResolution X) : left_derived.zero_to_self_app_inv F P ‚â´
  left_derived.zero_to_self_app F P = ùüô _ :=
begin
  dsimp [left_derived.zero_to_self_app, left_derived.zero_to_self_app_inv],
  rw [functor.map_id, category.comp_id, category.assoc, category.assoc, category.assoc,
    ‚Üê category.assoc (F.left_derived_obj_iso 0 P).inv, iso.inv_hom_id, category.id_comp,
    is_iso.inv_comp_eq, category.comp_id],
  ext,
  simp only [cokernel.œÄ_desc_assoc, category.assoc, cokernel.œÄ_desc, homology.desc',
    cokernel_comparison],
  rw [‚Üê category.assoc, ‚Üê category.assoc (homology_iso_cokernel_lift _ _ _).inv, iso.inv_hom_id,
    category.id_comp],
  simp only [category.assoc, cokernel.œÄ_desc, kernel.lift_Œπ_assoc, category.id_comp],
end

/-- Given `P : ProjectiveResolution X`, the isomorphism `(F.left_derived 0).obj X ‚âÖ F.obj X` if
`preserves_finite_colimits F`. -/
def left_derived.zero_to_self_app_iso [enough_projectives C] [preserves_finite_colimits F]
  {X : C} (P : ProjectiveResolution X) : (F.left_derived 0).obj X ‚âÖ F.obj X :=
{ hom := left_derived.zero_to_self_app _ P,
  inv := left_derived.zero_to_self_app_inv _ P,
  hom_inv_id' := left_derived.zero_to_self_app_comp_inv _ P,
  inv_hom_id' := left_derived.zero_to_self_app_inv_comp _ P }

/-- Given `P : ProjectiveResolution X` and `Q : ProjectiveResolution Y` and a morphism `f : X ‚ü∂ Y`,
naturality of the square given by `left_derived.zero_to_self_obj_hom. -/
lemma left_derived.zero_to_self_natural [enough_projectives C] {X : C} {Y : C} (f : X ‚ü∂ Y)
  (P : ProjectiveResolution X) (Q : ProjectiveResolution Y) :
  (F.left_derived 0).map f ‚â´ left_derived.zero_to_self_app F Q =
  left_derived.zero_to_self_app F P ‚â´ F.map f :=
begin
  dsimp only [left_derived.zero_to_self_app],
  let f‚ÇÅ := ProjectiveResolution.lift f P Q,
  rw [functor.left_derived_map_eq F 0 f f‚ÇÅ (by simp),
    category.assoc, category.assoc, ‚Üê category.assoc _ (F.left_derived_obj_iso 0 Q).hom,
    iso.inv_hom_id, category.id_comp, category.assoc, category.assoc],
  congr' 1,
  rw [functor.map_id, functor.map_id, category.id_comp, category.comp_id],
  dsimp only [homology_functor_map],
  ext,
  simp only [homological_complex.hom.sq_to_right, map_homological_complex_map_f,
    homology.œÄ'_map_assoc, homology.desc'_œÄ', kernel.lift_Œπ_assoc, category.assoc,
    homology.desc'_œÄ'_assoc],
  rw [‚Üê functor.map_comp, ‚Üê functor.map_comp],
  congr' 2,
  exact homological_complex.congr_hom (ProjectiveResolution.lift_commutes f P Q) 0
end

/-- The natural transformation `nat_trans (F.left_derived 0) F`. -/
def left_derived.zero_to_self [enough_projectives C] : (F.left_derived 0) ‚ü∂ F :=
{ app := Œª X, left_derived.zero_to_self_app F (ProjectiveResolution.of X),
  naturality' := Œª X Y f, left_derived.zero_to_self_natural F f (ProjectiveResolution.of X)
    (ProjectiveResolution.of Y) }

/-- Given `preserves_finite_colimits F`, the natural isomorphism `(F.left_derived 0) ‚âÖ F`. -/
def left_derived.zero_iso_self [enough_projectives C] [preserves_finite_colimits F] :
  (F.left_derived 0) ‚âÖ F :=
nat_iso.of_components (Œª X, left_derived.zero_to_self_app_iso _ (ProjectiveResolution.of X))
  (Œª X Y f, left_derived.zero_to_self_natural _ _ _ _)

section les

def Œ¥‚ÇÄ [enough_projectives C] [preserves_finite_colimits F] (A : short_exact_sequence C) :=
Œ¥ F 0 A ‚â´ (left_derived.zero_iso_self F).hom.app A.1

lemma seven_term_exact_seq [enough_projectives C] [preserves_finite_colimits F]
  (A : short_exact_sequence C) :
  exact_seq D [
    (F.left_derived 1).map A.f, (F.left_derived 1).map A.g,
    Œ¥‚ÇÄ F A,
    F.map A.f, F.map A.g, (0 : F.obj A.3 ‚ü∂ F.obj A.3)] :=
begin
  refine exact_seq.cons _ _ (exact_of_short_exact _ _ _) _ (exact_seq.cons _ _ _ _ _),
  { refine preadditive.exact_of_iso_of_exact' ((F.left_derived 1).map A.g) (Œ¥ F 0 A) _ _
      (iso.refl _) (iso.refl _) ((left_derived.zero_iso_self F).app A.1) (by simp) _ _,
    { dsimp [Œ¥‚ÇÄ], rw [category.id_comp] },
    { exact (exact_iff_exact_seq _ _).2 ((six_term_exact_seq F 0 A).extract 1 2) } },
  refine exact_seq.cons _ _ _ _ _,
  { refine preadditive.exact_of_iso_of_exact' (Œ¥ F 0 A) ((F.left_derived 0).map A.f) _ _
      (iso.refl _) ((left_derived.zero_iso_self F).app A.1) ((left_derived.zero_iso_self F).app A.2)
      _ (by simp) _,
    { dsimp [Œ¥‚ÇÄ], rw [category.id_comp] },
    { exact (exact_iff_exact_seq _ _).2 ((six_term_exact_seq F 0 A).extract 2 2) } },
  refine preserves_exact_seq _ (exact_seq.cons _ _ A.exact' _ ((exact_iff_exact_seq _ _).1 _)),
  refine ((abelian.tfae_epi A.3 A.g).out 0 2).1 A.epi',
end

end les

end functor.right_exact

end category_theory
