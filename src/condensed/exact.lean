import for_mathlib.Profinite.extend
import for_mathlib.AddCommGroup.exact

import condensed.ab
import pseudo_normed_group.bounded_limits
import condensed.extr.lift_comphaus
import condensed.projective_resolution
import condensed.kernel_comparison

.

universe u

open_locale nnreal

open category_theory category_theory.limits opposite pseudo_normed_group

namespace CompHausFiltPseuNormGrp‚ÇÅ

variables {A B C : CompHausFiltPseuNormGrp‚ÇÅ.{u}}

structure exact_with_constant (f : A ‚ü∂ B) (g : B ‚ü∂ C) (r : ‚Ñù‚â•0) : Prop :=
(exact : exact ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).map f) ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).map g))
(cond : ‚àÄ c : ‚Ñù‚â•0, g ‚Åª¬π' {0} ‚à© (filtration B c) ‚äÜ f '' (filtration A (r * c)))

lemma exact_with_constant_extend {A B C : Fintype ‚•§ CompHausFiltPseuNormGrp‚ÇÅ.{u}}
  (f : A ‚ü∂ B) (g : B ‚ü∂ C) (r : ‚Ñù‚â•0)
  (hfg : ‚àÄ S, exact_with_constant (f.app S) (g.app S) r) (S : Profinite) :
  exact_with_constant
   ((Profinite.extend_nat_trans f).app S) ((Profinite.extend_nat_trans g).app S) r :=
sorry

instance has_zero_nat_trans_CHFPNG‚ÇÅ {ùíû : Type*} [category ùíû]
  (A B : ùíû ‚•§ CompHausFiltPseuNormGrp‚ÇÅ.{u}) :
  has_zero (A ‚ü∂ B) :=
‚ü®‚ü®0, Œª S T f, by { ext t, exact (B.map f).map_zero.symm }‚ü©‚ü©

@[simp] lemma Profinite.extend_nat_trans_zero (A B : Fintype ‚•§ CompHausFiltPseuNormGrp‚ÇÅ.{u}) :
  Profinite.extend_nat_trans (0 : A ‚ü∂ B) = 0 :=
sorry

lemma exact_with_constant_extend_zero_left (A B C : Fintype ‚•§ CompHausFiltPseuNormGrp‚ÇÅ.{u})
  (g : B ‚ü∂ C) (r : ‚Ñù‚â•0)
  (hfg : ‚àÄ S, exact_with_constant (0 : A.obj S ‚ü∂ B.obj S) (g.app S) r) (S : Profinite) :
  exact_with_constant (0 : (Profinite.extend A).obj S ‚ü∂ (Profinite.extend B).obj S)
    ((Profinite.extend_nat_trans g).app S) r :=
begin
  have := exact_with_constant_extend (0 : A ‚ü∂ B) g r hfg S,
  simpa,
end

lemma exact_with_constant_extend_zero_right (A B C : Fintype ‚•§ CompHausFiltPseuNormGrp‚ÇÅ.{u})
  (f : A ‚ü∂ B) (r : ‚Ñù‚â•0)
  (hfg : ‚àÄ S, exact_with_constant (f.app S) (0 : B.obj S ‚ü∂ C.obj S) r) (S : Profinite) :
  exact_with_constant ((Profinite.extend_nat_trans f).app S)
    (0 : (Profinite.extend B).obj S ‚ü∂ (Profinite.extend C).obj S) r :=
begin
  have := exact_with_constant_extend f (0 : B ‚ü∂ C) r hfg S,
  simpa,
end

variables (C)

lemma exact_with_constant_of_epi (f : A ‚ü∂ B) [H : epi ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).map f)]
  (r : ‚Ñù‚â•0) (hf : ‚àÄ c, filtration B c ‚äÜ f '' (filtration A (r * c))) :
  exact_with_constant f (0 : B ‚ü∂ C) r :=
begin
  fsplit,
  { exact ((abelian.tfae_epi
      ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).obj C)
      ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).map f)).out 0 2).mp H, },
  { intro c, exact set.subset.trans (set.inter_subset_right _ _) (hf c), }
end

variables (A) {C}

lemma exact_with_constant_of_mono (g : B ‚ü∂ C) [hg : mono ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).map g)] :
  exact_with_constant (0 : A ‚ü∂ B) g 1 :=
begin
  fsplit,
  { exact ((abelian.tfae_mono
      ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).obj A)
      ((to_PNG‚ÇÅ ‚ãô PseuNormGrp‚ÇÅ.to_Ab).map g)).out 0 2).mp hg, },
  { rintro c x ‚ü®hx, -‚ü©,
    suffices : x = 0, { subst x, refine ‚ü®0, zero_mem_filtration _, rfl‚ü©, },
    simp only [set.mem_preimage, set.mem_singleton_iff] at hx,
    rw [AddCommGroup.mono_iff_injective, add_monoid_hom.injective_iff] at hg,
    exact hg _ hx, }
end

end CompHausFiltPseuNormGrp‚ÇÅ

namespace condensed

open CompHausFiltPseuNormGrp‚ÇÅ

lemma zero_iff_ExtrDisc {A B : Condensed.{u} Ab.{u+1}} (f : A ‚ü∂ B) :
  f = 0 ‚Üî (‚àÄ S : ExtrDisc, f.val.app (op S.val) = 0) :=
begin
  split,
  { rintros ‚ü®rfl‚ü©, simp },
  { intros h,
    apply (Condensed_ExtrSheafProd_equiv Ab).functor.map_injective,
    apply (ExtrSheafProd_to_presheaf Ab).map_injective,
    ext : 2,
    apply h }
end

lemma exact_iff_ExtrDisc {A B C : Condensed.{u} Ab.{u+1}} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
  exact f g ‚Üî ‚àÄ (S : ExtrDisc),
    exact (f.1.app $ ExtrDisc_to_Profinite.op.obj (op S))
          (g.1.app $ ExtrDisc_to_Profinite.op.obj (op S)) :=
begin
  simp only [abelian.exact_iff, zero_iff_ExtrDisc, forall_and_distrib],
  refine and_congr iff.rfl _,
  apply forall_congr,
  intro S,
  symmetry,
  rw [‚Üê cancel_epi (kernel_iso g S).hom, ‚Üê cancel_mono (cokernel_iso f S).hom],
  dsimp only [functor.op_obj, ExtrDisc_to_Profinite_obj],
  simp only [category.assoc, zero_comp, comp_zero],
  erw [kernel_iso_hom_assoc, cokernel_iso_hom],
  exact iff.rfl,
end

open comphaus_filtered_pseudo_normed_group

lemma exact_of_exact_with_constant {A B C : CompHausFiltPseuNormGrp‚ÇÅ.{u}}
  (f : A ‚ü∂ B) (g : B ‚ü∂ C) (r : ‚Ñù‚â•0) (hr : 1 ‚â§ r)
  (hfg : exact_with_constant f g r) :
  exact (to_Condensed.map f) (to_Condensed.map g) :=
begin
  rw exact_iff_ExtrDisc,
  intro S,
  simp only [subtype.val_eq_coe, to_Condensed_map, CompHausFiltPseuNormGrp.Presheaf.map_app,
    whisker_right_app, Ab.exact_ulift_map],
  rw AddCommGroup.exact_iff',
  split,
  { ext x s,
    simp only [subtype.val_eq_coe, CompHausFiltPseuNormGrp.presheaf.map_apply, function.comp_app,
      category_theory.comp_apply, AddCommGroup.zero_apply,
      strict_comphaus_filtered_pseudo_normed_group_hom.to_chfpsng_hom_to_fun],
    exact fun_like.congr_fun hfg.exact.w (x.1 s), },
  { rintro ‚ü®_, c, y‚ÇÄ : S.val ‚Üí filtration B c, hy‚ÇÄ, rfl‚ü© hy,
    dsimp at hy ‚ä¢,
    simp only [add_monoid_hom.mem_ker, add_monoid_hom.mem_range, function.comp,
      strict_comphaus_filtered_pseudo_normed_group_hom.to_chfpsng_hom_to_fun,
      CompHausFiltPseuNormGrp.presheaf.map_apply] at hy ‚ä¢,
    let f‚ÇÄ : (CompHaus.of $ filtration A (r * c)) ‚ü∂ (CompHaus.of $ filtration B (r * c)) :=
      (CompHausFiltPseuNormGrp‚ÇÅ.level.obj (r * c)).map f,
    let g‚ÇÄ : (CompHaus.of $ filtration B c) ‚ü∂ (CompHaus.of $ filtration C c) :=
      (CompHausFiltPseuNormGrp‚ÇÅ.level.obj c).map g,
    let K : set (filtration B c) := g‚ÇÄ ‚Åª¬π' {(0 : filtration C c)},
    have K_cmpt : is_compact K := (is_closed_singleton.preimage g‚ÇÄ.continuous).is_compact,
    rw is_compact_iff_compact_space at K_cmpt,
    have aux : fact (c ‚â§ r * c),
    { refine ‚ü®_‚ü©, transitivity 1 * c, rw one_mul, exact mul_le_mul' hr le_rfl },
    resetI,
    let Œ± : (CompHaus.of $ K) ‚ü∂ (CompHaus.of $ filtration B (r * c)) :=
      ‚ü®cast_le ‚àò (coe : K ‚Üí filtration B c), (continuous_cast_le _ _).comp continuous_subtype_val‚ü©,
    let Z := pullback Œ± f‚ÇÄ,
    have hZ : function.surjective (pullback.fst : Z ‚ü∂ _),
    { rintro (b : K),
      have hb : (b : B) ‚àà g ‚Åª¬π' {0} ‚à© filtration B c,
      { refine ‚ü®_, b.1.2‚ü©, have := b.2, dsimp [K] at this,
        simp only [set.mem_preimage, set.mem_singleton_iff] at this ‚ä¢,
        exact congr_arg coe this, },
      obtain ‚ü®a, ha‚ü© := hfg.cond c hb,
      let t : CompHaus.of punit ‚ü∂ Z := pullback.lift
        ‚ü®Œª _, b, continuous_const‚ü© ‚ü®Œª _, ‚ü®a, ha.1‚ü©, continuous_const‚ü© _,
      swap,
      { ext ‚ü®‚ü©,
        simp only [CompHaus.coe_comp, continuous_map.coe_mk, function.comp_app, coe_cast_le],
        exact ha.2.symm, },
      refine ‚ü®t punit.star, _‚ü©,
      rw [‚Üê category_theory.comp_apply, pullback.lift_fst],
      refl, },
    let y‚ÇÄ' : S.val ‚Üí K := Œª s, ‚ü®y‚ÇÄ s, _‚ü©,
    swap, { ext, rw subtype.ext_iff at hy, exact congr_fun hy s, },
    have hy‚ÇÄ' : continuous y‚ÇÄ' := continuous_subtype_mk _ hy‚ÇÄ,
    let x : S.val.to_CompHaus ‚ü∂ Z := ExtrDisc.lift' _ hZ ‚ü®y‚ÇÄ', hy‚ÇÄ'‚ü©,
    have hx' : x ‚â´ (pullback.fst : Z ‚ü∂ _) = ‚ü®y‚ÇÄ', hy‚ÇÄ'‚ü© := ExtrDisc.lift_lifts' _ _ _,
    let x‚ÇÄ : S.val ‚Üí filtration A (r * c) := (pullback.snd : Z ‚ü∂ _) ‚àò x,
    have hx‚ÇÄ : continuous x‚ÇÄ := (continuous_map.continuous _).comp x.continuous,
    have hfx‚ÇÄ : ‚àÄ s : S.val, f (x‚ÇÄ s) = y‚ÇÄ s,
    { intro s,
      have := (@pullback.condition _ _ _ _ _ Œ± f‚ÇÄ _),
      rw fun_like.ext_iff at this,
      convert (congr_arg (coe : filtration B _ ‚Üí B) (this (x s))).symm using 1,
      rw [fun_like.ext_iff] at hx',
      simp only [coe_comp, function.comp_apply] at hx' ‚ä¢,
      rw hx', refl },
    refine ‚ü®‚ü®_, _, x‚ÇÄ, hx‚ÇÄ, rfl‚ü©, _‚ü©,
    ext s,
    exact hfx‚ÇÄ s, }
end
.

@[simp] lemma to_Condensed_map_zero (A B : CompHausFiltPseuNormGrp‚ÇÅ.{u}) :
  to_Condensed.map (0 : A ‚ü∂ B) = 0 :=
by { ext S s x, refl, }

lemma mono_to_Condensed_map {A B : CompHausFiltPseuNormGrp‚ÇÅ.{u}}
  (f : A ‚ü∂ B) (hf : exact_with_constant (0 : A ‚ü∂ A) f 1) :
  mono (to_Condensed.map f) :=
begin
  refine ((abelian.tfae_mono (to_Condensed.obj A) (to_Condensed.map f)).out 2 0).mp _,
  have := exact_of_exact_with_constant (0 : A ‚ü∂ A) f 1 le_rfl hf,
  simpa only [to_Condensed_map_zero],
end

lemma epi_to_Condensed_map {A B : CompHausFiltPseuNormGrp‚ÇÅ.{u}}
  (f : A ‚ü∂ B) (r : ‚Ñù‚â•0) (hr : 1 ‚â§ r) (hf : exact_with_constant f (0 : B ‚ü∂ B) r) :
  epi (to_Condensed.map f) :=
begin
  refine ((abelian.tfae_epi (to_Condensed.obj B) (to_Condensed.map f)).out 2 0).mp _,
  have := exact_of_exact_with_constant f (0 : B ‚ü∂ B) r hr hf,
  simpa only [to_Condensed_map_zero]
end

end condensed
