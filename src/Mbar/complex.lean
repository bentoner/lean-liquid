import for_mathlib.equalizers
import Mbar.Mbar_pow
import system_of_complexes

/-!
# The system of complexes associated with `V` and `Mbar_le r' S c`

In this file we have two main definitions:

* `LCC_Mbar_pow_Tinv` is the normed abelian group `V-hat(Mbar_{r'}(S)_{‚â§c}^a)^{T‚Åª¬π}`
* `Mbar_system` packages all those normed abelian groups together
  into a system of complexes.
  Those complexes are constructed using Breen--Deligne data.
-/

open_locale classical nnreal
noncomputable theory

open opposite breen_deligne category_theory category_theory.limits

variables (BD : package) (c' : ‚Ñï ‚Üí ‚Ñù‚â•0) [BD.suitable c']
variables (V : NormedGroup) (S : Type*) (r r' c c‚ÇÅ c‚ÇÇ c‚ÇÉ c‚ÇÑ : ‚Ñù‚â•0) (a : ‚Ñï) [fintype S]

/-- The space `V-hat(Mbar_{r'}(S)_{‚â§c}^a)^{T‚Åª¬π}`, defined by taking `T‚Åª¬π`-invariants
for two different actions by `T‚Åª¬π`:

* The first comes from the action of `T‚Åª¬π` on `Mbar_{r'}(S)`.
* The second comes from the action of `T‚Åª¬π` on `V`.

We take the equalizer of those two actions.

See the lines just above Definition 9.3 of [Analytic]. -/
def LCC_Mbar_pow_Tinv [fact (0 < r)] [fact (0 < r')] [fact (r' ‚â§ 1)] [normed_with_aut r V] :
  NormedGroup :=
equalizer (LCC_Mbar_pow.Tinv V S r' c a) (normed_with_aut.T.inv ‚â´ (LCC_Mbar_pow.res V S r' _ _ a))

variables [fact (0 < r)] [fact (0 < r')] [fact (r' ‚â§ 1)] [normed_with_aut r V]

namespace LCC_Mbar_pow_Tinv

/-- The restriction map. -/
def res [fact (c‚ÇÅ ‚â§ c‚ÇÇ)] :
  LCC_Mbar_pow_Tinv V S r r' c‚ÇÇ a ‚ü∂ LCC_Mbar_pow_Tinv V S r r' c‚ÇÅ a :=
equalizer.map (LCC_Mbar_pow.res _ _ _ _ _ _) (LCC_Mbar_pow.res _ _ _ _ _ _)
(by rw LCC_Mbar_pow.Tinv_res)
begin
  haveI : fact (c‚ÇÅ ‚â§ r'‚Åª¬π * c‚ÇÇ) :=
    le_trans ‚Äπc‚ÇÅ ‚â§ c‚ÇÇ‚Ä∫ (show fact (c‚ÇÇ ‚â§ r'‚Åª¬π * c‚ÇÇ), by apply_instance),
  rw [category.assoc, LCC_Mbar_pow.res_comp_res,
      ‚Üê LCC_Mbar_pow.T_inv_res_assoc, LCC_Mbar_pow.res_comp_res]
end

lemma res_comp_res [fact (c‚ÇÅ ‚â§ c‚ÇÇ)] [fact (c‚ÇÇ ‚â§ c‚ÇÉ)] [fact (c‚ÇÅ ‚â§ c‚ÇÉ)] :
  res V S r r' c‚ÇÇ c‚ÇÉ a ‚â´ res V S r r' c‚ÇÅ c‚ÇÇ a = res V S r r' c‚ÇÅ c‚ÇÉ a :=
by simp only [res, equalizer.map_comp_map, LCC_Mbar_pow.res_comp_res]

@[simp] lemma res_refl [normed_with_aut r V] : res V S r r' c c a = ùüô _ :=
by { simp only [res, equalizer.map_id, LCC_Mbar_pow.res_refl], refl }

end LCC_Mbar_pow_Tinv

namespace breen_deligne

namespace universal_map

variables {l m n : ‚Ñï}

/-- `f.eval_Mbar_pow_Tinv` for `f : universal_map m n` is the
homomorphism `LCC_Mbar_pow_Tinv V S r r' c‚ÇÇ n ‚ü∂ LCC_Mbar_pow_Tinv V S r r' c‚ÇÅ m`
induced by `f.eval_Mbar_pow`.

The latter is the sum of maps `g.eval_Mbar_pow`,
where `g` is a `basic_universal_map m n` (aka matrix)
occuring in the formal sum `f`.

TODO(jmc): I do not know a precise reference for this definition in [Analytic]. -/
def eval_Mbar_pow_Tinv (f : universal_map m n) [f.suitable c‚ÇÅ c‚ÇÇ] :
  LCC_Mbar_pow_Tinv V S r r' c‚ÇÇ n ‚ü∂ LCC_Mbar_pow_Tinv V S r r' c‚ÇÅ m :=
equalizer.map
  (f.eval_Mbar_pow V S r' ((r'‚Åª¬π * c‚ÇÅ)) ((r'‚Åª¬π * c‚ÇÇ)))
  (f.eval_Mbar_pow V S r' c‚ÇÅ c‚ÇÇ)
  (by rw eval_Mbar_pow_comp_Tinv)
  (by rw [category.assoc, ‚Üê eval_Mbar_pow_comp_res V S r' c‚ÇÅ c‚ÇÇ (r'‚Åª¬π * c‚ÇÅ) (r'‚Åª¬π * c‚ÇÇ) f,
      eval_Mbar_pow_comp_T_inv_assoc])

@[simp] lemma eval_Mbar_pow_Tinv_zero :
  (0 : universal_map m n).eval_Mbar_pow_Tinv V S r r' c‚ÇÅ c‚ÇÇ = 0 :=
begin
  apply equalizer.hom_ext,
  simp only [eval_Mbar_pow_Tinv, eval_Mbar_pow_zero, zero_comp, equalizer.map_Œπ, comp_zero]
end

lemma eval_Mbar_pow_Tinv_comp (g : universal_map m n) (f : universal_map l m)
  [g.suitable c‚ÇÇ c‚ÇÉ] [f.suitable c‚ÇÅ c‚ÇÇ] [(comp g f).suitable c‚ÇÅ c‚ÇÉ] :
  (comp g f).eval_Mbar_pow_Tinv V S r r' c‚ÇÅ c‚ÇÉ =
    g.eval_Mbar_pow_Tinv V S r r' c‚ÇÇ c‚ÇÉ ‚â´ f.eval_Mbar_pow_Tinv V S r r' c‚ÇÅ c‚ÇÇ :=
by simp only [eval_Mbar_pow_Tinv, equalizer.map_comp_map, ‚Üê eval_Mbar_pow_comp]

lemma eval_Mbar_pow_Tinv_comp_res (f : universal_map m n)
  [f.suitable c‚ÇÅ c‚ÇÇ] [f.suitable c‚ÇÉ c‚ÇÑ] [fact (c‚ÇÅ ‚â§ c‚ÇÉ)] [fact (c‚ÇÇ ‚â§ c‚ÇÑ)] :
  f.eval_Mbar_pow_Tinv V S r r' c‚ÇÉ c‚ÇÑ ‚â´ LCC_Mbar_pow_Tinv.res V S r r' c‚ÇÅ c‚ÇÉ m =
  LCC_Mbar_pow_Tinv.res V S r r' c‚ÇÇ c‚ÇÑ n ‚â´ f.eval_Mbar_pow_Tinv V S r r' c‚ÇÅ c‚ÇÇ :=
begin
  delta eval_Mbar_pow_Tinv LCC_Mbar_pow_Tinv.res,
  rw [equalizer.map_comp_map, equalizer.map_comp_map],
  congr' 1; apply eval_Mbar_pow_comp_res
end

end universal_map

end breen_deligne

/-!
## The system of complexes associated with `V` and `Mbar S`

We are now ready to define the system of complexes occurring in the statement
of Theorems 9.4 and 9.5 of [Analytic].

We do this in two steps: first we define `Mbar_complex`,
which is the complex for a given `c : ‚Ñù‚â•0`.
Afterwards, we pack these complexes together in the system `Mbar_system`.
-/

open breen_deligne

/-- The complex
`V-hat(Mbar_{r'}(S)_{‚â§c})^{T‚Åª¬π} ‚ü∂ V-hat(Mbar_{r'}(S)_{‚â§c_1c}^2)^{T‚Åª¬π} ‚ü∂ ...`

These complexes are packed together in `Mbar_system` into a system of complexes,
as they occur in Theorems 9.4 and 9.5 of [Analytic]. -/
def Mbar_complex (BD : breen_deligne.package) (c' : ‚Ñï ‚Üí ‚Ñù‚â•0) [BD.suitable c'] :
  cochain_complex NormedGroup :=
{ /- the objects -/
  X := int.extend_from_nat 0 $ Œª i, LCC_Mbar_pow_Tinv V S r r' (c * c' i) (BD.rank i),
  /- the differentials -/
  d := int.extend_from_nat 0 $ Œª i, (BD.map i).eval_Mbar_pow_Tinv V S r r' (c * c' (i+1)) (c * c' i),
  d_squared' := /- d^2 = 0 -/
  begin
    ext1 ‚ü®i‚ü©,
    { dsimp,
      simp only [pi.comp_apply, pi.zero_apply],
      erw ‚Üê universal_map.eval_Mbar_pow_Tinv_comp V S r r' _ (c * c' (i+1)) _ (BD.map i) (BD.map (i+1)),
      simp only [BD.map_comp_map, universal_map.eval_Mbar_pow_Tinv_zero],
      apply_instance },
    { show 0 ‚â´ _ = 0, rw [zero_comp] }
  end }

@[simp] lemma Mbar_complex.d_neg_succ_of_nat
  (BD : breen_deligne.package) (c' : ‚Ñï ‚Üí ‚Ñù‚â•0) [BD.suitable c'] (n : ‚Ñï) :
  (Mbar_complex V S r r' c BD c').d -[1+n] = 0 := rfl

/-- The system of complexes
`V-hat(Mbar_{r'}(S)_{‚â§c})^{T‚Åª¬π} ‚ü∂ V-hat(Mbar_{r'}(S)_{‚â§c_1c}^2)^{T‚Åª¬π} ‚ü∂ ...`
occurring in Theorems 9.4 and 9.5 of [Analytic]. -/
def Mbar_system (BD : breen_deligne.package) (c' : ‚Ñï ‚Üí ‚Ñù‚â•0) [BD.suitable c'] :
  system_of_complexes :=
{ /- the objects, aka the constituent complexes -/
  obj := Œª c, Mbar_complex V S r r' (unop c : ‚Ñù‚â•0) BD c',
  /- the restriction maps -/
  map := Œª c‚ÇÇ c‚ÇÅ h,
  { f := int.extend_from_nat 0 $ Œª i,
    by { haveI : fact (((unop c‚ÇÅ : ‚Ñù‚â•0) : ‚Ñù) ‚â§ (unop c‚ÇÇ : ‚Ñù‚â•0)) := h.unop.down.down,
      exact LCC_Mbar_pow_Tinv.res V S r r' _ _ (BD.rank i) },
    comm' :=
    begin
      ext1 ‚ü®i‚ü©,
      { dsimp [int.extend_from_nat],
        apply universal_map.eval_Mbar_pow_Tinv_comp_res },
      { dsimp [int.extend_from_nat],
        simp only [Mbar_complex.d_neg_succ_of_nat, zero_comp] }
    end },
  map_id' := /- the restriction map for `c ‚â§ c` is the identity -/
  begin
    intro c,
    ext ‚ü®i‚ü© : 2,
    { dsimp [int.extend_from_nat],
      rw LCC_Mbar_pow_Tinv.res_refl V S r r' _ _, refl },
    { dsimp [int.extend_from_nat], ext }
  end,
  map_comp' := /- composition of transition maps is a transition map -/
  begin
    intros c‚ÇÉ c‚ÇÇ c‚ÇÅ h h',
    haveI H' : fact (((unop c‚ÇÅ : ‚Ñù‚â•0) : ‚Ñù) ‚â§ (unop c‚ÇÇ : ‚Ñù‚â•0)) := h'.unop.down.down,
    haveI H : fact (((unop c‚ÇÇ : ‚Ñù‚â•0) : ‚Ñù) ‚â§ (unop c‚ÇÉ : ‚Ñù‚â•0)) := h.unop.down.down,
    have : fact (((unop c‚ÇÅ : ‚Ñù‚â•0) : ‚Ñù) ‚â§ (unop c‚ÇÉ : ‚Ñù‚â•0)) := le_trans H' H,
    ext ‚ü®i‚ü© : 2,
    { dsimp [int.extend_from_nat],
      rw LCC_Mbar_pow_Tinv.res_comp_res V S r r' _ _ _ _ },
    { dsimp [int.extend_from_nat],
      rw zero_comp },
  end }
